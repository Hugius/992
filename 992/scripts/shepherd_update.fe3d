70 59
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "shepherd" OR _isFirstUpdate IS <true>
  fe3d:quad2d_set_visible("shepherd_worker", <false>)
  fe3d:quad2d_set_visible("shepherd_tool", <false>)
  fe3d:quad2d_set_visible("shepherd_time", <false>)
  fe3d:quad2d_set_visible("shepherd_convert", <false>)
  fe3d:quad2d_set_visible("shepherd_wool", <false>)
  fe3d:text2d_set_visible("shepherd_worker", <false>)
  fe3d:text2d_set_visible("shepherd_tool", <false>)
  fe3d:text2d_set_visible("shepherd_wool", <false>)

/// Show GUI
IF _selectedBuildingType IS "shepherd"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    IF hasTool IS <true>
      fe3d:quad2d_set_visible("shepherd_time", <true>)
      fe3d:quad2d_set_visible("shepherd_convert", <true>)
      fe3d:quad2d_set_visible("shepherd_wool", <true>)
      fe3d:text2d_set_visible("shepherd_wool", <true>)
    ELSE
      fe3d:quad2d_set_visible("shepherd_tool", <true>)
      fe3d:text2d_set_visible("shepherd_tool", <true>)
      IF _totalTools MORE 0
        ALT _cursorType = "HAND"
        DEF BOL isLmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
        IF isLmbPressed IS <true>
          SUB _totalTools 1
          PUSH _toolBuildingIds _selectedBuildingId
  ELSE
    fe3d:quad2d_set_visible("shepherd_worker", <true>)
    fe3d:text2d_set_visible("shepherd_worker", <true>)

/// Update shepherds
DEF STR sheepId = ""
DEF DEC growth = 0.0
DEF INT maxShepherdIndex = misc:list_size("_shepherdIds")
DEF INT shepherdIndex = 0
LOOP
  IF shepherdIndex IS maxShepherdIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _shepherdIds[shepherdIndex])
  ALT hasTool = misc:list_contains("_toolBuildingIds", _shepherdIds[shepherdIndex])
  IF hasWorker IS <true> AND hasTool IS <true>
    IF _shepherdGrowths[shepherdIndex] IS 0.0
      ADD _shepherdGrowths[shepherdIndex] _minShepherdGrowth
    ELIF _shepherdGrowths[shepherdIndex] MORE _maxShepherdGrowth
      IF _shepherdTimers[shepherdIndex] IS _productionTime
        ALT _shepherdTimers[shepherdIndex] = 0
        ADD _totalWool 1
      ELSE
        ADD _shepherdTimers[shepherdIndex] 1
  IF _shepherdGrowths[shepherdIndex] MORE 0.0 AND _shepherdGrowths[shepherdIndex] LESS _maxShepherdGrowth
    DEF INT time = _growthTime
    DEF DEC speed = _maxShepherdGrowth
    CAST time DEC
    DIV speed time
    ADD _shepherdGrowths[shepherdIndex] speed
  ALT growth = _shepherdGrowths[shepherdIndex]
  ALT sheepId = misc:string_concat(_shepherdIds[shepherdIndex], "_1")
  fe3d:model_set_base_size(sheepId, growth, growth, growth)
  ALT sheepId = misc:string_concat(_shepherdIds[shepherdIndex], "_2")
  fe3d:model_set_base_size(sheepId, growth, growth, growth)
  ADD shepherdIndex 1

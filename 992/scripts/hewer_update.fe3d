26 52
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "hewer" OR _isFirstUpdate IS <true>
  fe3d:quad2d_set_visible("hewer_worker", <false>)
  fe3d:quad2d_set_visible("hewer_tool", <false>)
  fe3d:quad2d_set_visible("hewer_time", <false>)
  fe3d:quad2d_set_visible("hewer_convert", <false>)
  fe3d:quad2d_set_visible("hewer_stone", <false>)
  fe3d:text2d_set_visible("hewer_worker", <false>)
  fe3d:text2d_set_visible("hewer_tool", <false>)
  fe3d:text2d_set_visible("hewer_stone", <false>)

/// Show GUI
IF _selectedBuildingType IS "hewer"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    IF hasTool IS <true>
      fe3d:quad2d_set_visible("hewer_time", <true>)
      fe3d:quad2d_set_visible("hewer_convert", <true>)
      fe3d:quad2d_set_visible("hewer_stone", <true>)
      fe3d:text2d_set_visible("hewer_stone", <true>)
    ELSE
      fe3d:quad2d_set_visible("hewer_tool", <true>)
      fe3d:text2d_set_visible("hewer_tool", <true>)
      IF _totalTools MORE 0
        ALT _cursorType = "HAND"
        DEF BOL isLmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
        IF isLmbPressed IS <true>
          SUB _totalTools 1
          PUSH _toolBuildingIds _selectedBuildingId
  ELSE
    fe3d:quad2d_set_visible("hewer_worker", <true>)
    fe3d:text2d_set_visible("hewer_worker", <true>)

/// Update hewers
DEF LST rockIds = {}
DEF STR closestRockId = ""
DEF DEC closestRockDistance = 0.0
DEF DEC rockDistance = 0.0
DEF DEC hX = 0.0
DEF DEC hY = 0.0
DEF DEC hZ = 0.0
DEF DEC rX = 0.0
DEF DEC rY = 0.0
DEF DEC rZ = 0.0
DEF INT maxHewerIndex = misc:list_size("_hewerIds")
DEF INT maxRockIndex = 0
DEF INT hewerIndex = 0
DEF INT rockIndex = 0
LOOP
  IF hewerIndex IS maxHewerIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _hewerIds[hewerIndex])
  ALT hasTool = misc:list_contains("_toolBuildingIds", _hewerIds[hewerIndex])
  IF hasWorker IS <true> AND hasTool IS <true>
    IF _hewerTimers[hewerIndex] IS _productionTime
      ALT _hewerTimers[hewerIndex] = 0
      ALT hX = fe3d:model_get_base_position_x(_hewerIds[hewerIndex])
      ALT hY = fe3d:model_get_base_position_y(_hewerIds[hewerIndex])
      ALT hZ = fe3d:model_get_base_position_z(_hewerIds[hewerIndex])
      ALT rockIds = fe3d:model_find_ids("rock")
      ALT maxRockIndex = misc:list_size("rockIds")
      ALT rockIndex = 0
      LOOP
        IF rockIndex IS maxRockIndex
          BREAK
        ALT rX = fe3d:model_get_base_position_x(rockIds[rockIndex])
        ALT rY = fe3d:model_get_base_position_y(rockIds[rockIndex])
        ALT rZ = fe3d:model_get_base_position_z(rockIds[rockIndex])
        ALT rockDistance = math:vector_distance(hX, hY, hZ, rX, rY, rZ)
        IF closestRockId IS "" OR rockDistance LESS closestRockDistance
          ALT closestRockId = rockIds[rockIndex]
          ALT closestRockDistance = rockDistance
        ADD rockIndex 1
      IF closestRockId NOT ""
        fe3d:model_delete(closestRockId)
        ADD _totalWood 1
    ELSE
      ADD _hewerTimers[hewerIndex] 1
  ADD hewerIndex 1

62 24
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "woodcutter" OR _isFirstUpdate IS <true>
  fe3d:quad2d_set_visible("woodcutter_worker", <false>)
  fe3d:quad2d_set_visible("woodcutter_tool", <false>)
  fe3d:quad2d_set_visible("woodcutter_wood", <false>)
  fe3d:quad2d_set_visible("woodcutter_convert", <false>)
  fe3d:quad2d_set_visible("woodcutter_planks", <false>)
  fe3d:quad2d_set_visible("woodcutter_firewood", <false>)
  fe3d:text2d_set_visible("woodcutter_worker", <false>)
  fe3d:text2d_set_visible("woodcutter_tool", <false>)
  fe3d:text2d_set_visible("woodcutter_wood", <false>)
  fe3d:text2d_set_visible("woodcutter_planks", <false>)
  fe3d:text2d_set_visible("woodcutter_firewood", <false>)

/// Show GUI
IF _selectedBuildingType IS "woodcutter"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    IF hasTool IS <true>
      fe3d:quad2d_set_visible("woodcutter_wood", <true>)
      fe3d:quad2d_set_visible("woodcutter_convert", <true>)
      fe3d:quad2d_set_visible("woodcutter_planks", <true>)
      fe3d:quad2d_set_visible("woodcutter_firewood", <true>)
      fe3d:text2d_set_visible("woodcutter_wood", <true>)
      fe3d:text2d_set_visible("woodcutter_planks", <true>)
      fe3d:text2d_set_visible("woodcutter_firewood", <true>)
    ELSE
      fe3d:quad2d_set_visible("woodcutter_tool", <true>)
      fe3d:text2d_set_visible("woodcutter_tool", <true>)
      IF _totalTools MORE 0
        ALT _cursorType = "HAND"
        DEF BOL isLmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
        IF isLmbPressed IS <true>
          SUB _totalTools 1
          PUSH _toolBuildingIds _selectedBuildingId
  ELSE
    fe3d:quad2d_set_visible("woodcutter_worker", <true>)
    fe3d:text2d_set_visible("woodcutter_worker", <true>)

/// Update woodcutters
DEF INT maxWoodcutterIndex = misc:list_size("_woodcutterIds")
DEF INT woodcutterIndex = 0
LOOP
  IF woodcutterIndex IS maxWoodcutterIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _woodcutterIds[woodcutterIndex])
  ALT hasTool = misc:list_contains("_toolBuildingIds", _woodcutterIds[woodcutterIndex])
  IF hasWorker IS <true> AND hasTool IS <true>
    IF _woodcutterTimers[woodcutterIndex] IS 0
      IF _totalWood MORE 0
        SUB _totalWood 1
        ADD _woodcutterTimers[woodcutterIndex] 1
    ELIF _woodcutterTimers[woodcutterIndex] IS _productionTime
      ALT _woodcutterTimers[woodcutterIndex] = 0
      ADD _totalPlanks 5
      ADD _totalFirewood 5
    ELSE
      ADD _woodcutterTimers[woodcutterIndex] 1
  ADD woodcutterIndex 1

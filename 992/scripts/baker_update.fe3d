56 40
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "baker" OR _isFirstUpdate IS <true>
  fe3d:quad2d_set_visible("baker_worker", <false>)
  fe3d:quad2d_set_visible("baker_flour", <false>)
  fe3d:quad2d_set_visible("baker_firewood", <false>)
  fe3d:quad2d_set_visible("baker_coal", <false>)
  fe3d:quad2d_set_visible("baker_convert", <false>)
  fe3d:quad2d_set_visible("baker_bread", <false>)
  fe3d:text2d_set_visible("baker_worker", <false>)
  fe3d:text2d_set_visible("baker_flour", <false>)
  fe3d:text2d_set_visible("baker_firewood", <false>)
  fe3d:text2d_set_visible("baker_coal", <false>)
  fe3d:text2d_set_visible("baker_bread", <false>)

/// Show GUI
IF _selectedBuildingType IS "baker"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    fe3d:quad2d_set_visible("baker_flour", <true>)
    fe3d:quad2d_set_visible("baker_firewood", <true>)
    fe3d:quad2d_set_visible("baker_coal", <true>)
    fe3d:quad2d_set_visible("baker_convert", <true>)
    fe3d:quad2d_set_visible("baker_bread", <true>)
    fe3d:text2d_set_visible("baker_flour", <true>)
    fe3d:text2d_set_visible("baker_firewood", <true>)
    fe3d:text2d_set_visible("baker_coal", <true>)
    fe3d:text2d_set_visible("baker_bread", <true>)
  ELSE
    fe3d:quad2d_set_visible("baker_worker", <true>)
    fe3d:text2d_set_visible("baker_worker", <true>)

/// Update bakers
DEF INT maxBakerIndex = misc:list_size("_bakerIds")
DEF INT bakerIndex = 0
DEF INT randomNumber = 0
LOOP
  IF bakerIndex IS maxBakerIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _bakerIds[bakerIndex])
  IF hasWorker IS <true>
    IF _bakerTimers[bakerIndex] IS 0
      IF _totalFlour MORE 0
        ALT randomNumber = math:random_integer(1, 2)
        IF randomNumber IS 1 AND _totalFirewood MORE 0
          SUB _totalFlour 1
          SUB _totalFirewood 1
          ADD _bakerTimers[bakerIndex] 1
        ELIF randomNumber IS 2 AND _totalCoal MORE 0
          SUB _totalFlour 1
          SUB _totalCoal 1
          ADD _bakerTimers[bakerIndex] 1
    ELIF _bakerTimers[bakerIndex] IS _productionTime
      ALT _bakerTimers[bakerIndex] = 0
      ADD _totalBread 5
    ELSE
      ADD _bakerTimers[bakerIndex] 1
  ADD bakerIndex 1

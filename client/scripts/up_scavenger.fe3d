39 17
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "scavenger" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("scavenger_worker", <false>)
    fe3d:quad2d_set_visible("scavenger_tool", <false>)
    fe3d:quad2d_set_visible("scavenger_info", <false>)
    fe3d:text2d_set_visible("scavenger_worker", <false>)
    fe3d:text2d_set_visible("scavenger_tool", <false>)
    fe3d:text2d_set_visible("scavenger_info", <false>)

/// Show GUI
IF _selectedBuildingType IS "scavenger"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasTool IS <true>
            fe3d:quad2d_set_visible("scavenger_info", <true>)
            fe3d:text2d_set_visible("scavenger_info", <true>)
        ELSE
            fe3d:quad2d_set_visible("scavenger_tool", <true>)
            fe3d:text2d_set_visible("scavenger_tool", <true>)
            IF _totalTools MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalTools 1
                    PUSH _toolBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("scavenger_worker", <true>)
        fe3d:text2d_set_visible("scavenger_worker", <true>)

/// Update scavengers
DEF INT maxIndex = misc:list_size("_scavengerIds")
DEF INT index = 0
DEF BOL decreaseSicknessChance = <false>
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _scavengerIds[index])
    ALT hasTool = misc:list_contains("_toolBuildingIds", _scavengerIds[index])
    IF hasWorker IS <true> AND hasTool IS <true>
        ALT decreaseSicknessChance = <true>
        BREAK
    ADD index 1

IF decreaseSicknessChance IS <true>
    SUB _sicknessCatchChance 5

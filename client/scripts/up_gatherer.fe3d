13 53
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "gatherer" OR _firstUpdate IS <true>
  fe3d:quad2d_set_visible("gatherer_worker", <false>)
  fe3d:quad2d_set_visible("gatherer_time", <false>)
  fe3d:quad2d_set_visible("gatherer_convert", <false>)
  fe3d:quad2d_set_visible("gatherer_seeds", <false>)
  fe3d:quad2d_set_visible("gatherer_mushrooms", <false>)
  fe3d:text2d_set_visible("gatherer_worker", <false>)
  fe3d:text2d_set_visible("gatherer_seeds", <false>)
  fe3d:text2d_set_visible("gatherer_mushrooms", <false>)

/// Show GUI
IF _selectedBuildingType IS "gatherer"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    fe3d:quad2d_set_visible("gatherer_time", <true>)
    fe3d:quad2d_set_visible("gatherer_convert", <true>)
    fe3d:quad2d_set_visible("gatherer_seeds", <true>)
    fe3d:quad2d_set_visible("gatherer_mushrooms", <true>)
    fe3d:text2d_set_visible("gatherer_seeds", <true>)
    fe3d:text2d_set_visible("gatherer_mushrooms", <true>)
  ELSE
    fe3d:quad2d_set_visible("gatherer_worker", <true>)
    fe3d:text2d_set_visible("gatherer_worker", <true>)

/// Update gatherers
DEF INT maxGathererIndex = misc:list_size("_gathererIds")
DEF INT gathererIndex = 0
LOOP
  IF gathererIndex IS maxGathererIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _gathererIds[gathererIndex])
  IF hasWorker IS <true>
    IF _gathererTimers[gathererIndex] IS _productionTime
      ALT _gathererTimers[gathererIndex] = 0
      ADD _totalSeeds 1
      ADD _totalMushrooms 1
    ELSE
      ADD _gathererTimers[gathererIndex] 1
  ADD gathererIndex 1

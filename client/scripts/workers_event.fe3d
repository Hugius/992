75 8
META script_type_update
META script_state_wait

/// Values
BOL workerIsDead = <false>

/// Update event timer
IF _workerEventTimer IS _workerEventTimespan
    EDIT _workerEventTimer = 0
ELSE
    ADD _workerEventTimer 1
    EXIT

/// Death by suicide
IF workerIsDead IS <false>
    PASS

/// Death by sickness
IF workerIsDead IS <false>
    IF _workerHealths[_workerEventIndex] IS <false>
        IF _totalMedicine MORE 0
            SUB _totalMedicine 1
            EDIT _workerHealths[_workerEventIndex] = <true>
        ELSE
            EDIT workerIsDead = <true>
            STR workerName = _workerNames[_workerEventIndex]
            STR eventMessage = misc:string_concat(workerName, " died of sickness")
            fe3d:print(eventMessage)

/// Death by dehydration
IF workerIsDead IS <false>
    IF _totalWater MORE 0
        SUB _totalWater 1
    ELIF _totalAle MORE 0
        SUB _totalAle 1
    ELIF _totalMilk MORE 0
        SUB _totalMilk 1
    ELSE
        EDIT workerIsDead = <true>
        STR workerName = _workerNames[_workerEventIndex]
        STR eventMessage = misc:string_concat(workerName, " died of dehydration")
        fe3d:print(eventMessage)

/// Death by starvation
IF workerIsDead IS <false>
    IF _totalSmokedMeat MORE 0
        SUB _totalSmokedMeat 1
    ELIF _totalSmokedFish MORE 0
        SUB _totalSmokedFish 1
    ELIF _totalBread MORE 0
        SUB _totalBread 1
    ELIF _totalVegetables MORE 0
        SUB _totalVegetables 1
    ELIF _totalEggs MORE 0
        SUB _totalEggs 1
    ELIF _totalRawMeat MORE 0
        SUB _totalRawMeat 1
    ELIF _totalRawFish MORE 0
        SUB _totalRawFish 1
    ELIF _totalHoney MORE 0
        SUB _totalHoney 1
    ELIF _totalFruit MORE 0
        SUB _totalFruit 1
    ELSE
        EDIT workerIsDead = <true>
        STR workerName = _workerNames[_workerEventIndex]
        STR eventMessage = misc:string_concat(workerName, " died of starvation")
        fe3d:print(eventMessage)

/// Catching the sickness
IF workerIsDead IS <false>
    INT randomNumber = misc:random_integer(1, 100)
    INT sicknessChance = 100
    SUB sicknessChance _sicknessChance
    IF randomNumber MORE sicknessChance
        EDIT _workerHealths[_workerEventIndex] = <false>
        STR workerName = _workerNames[_workerEventIndex]
        STR eventMessage = misc:string_concat(workerName, " caught the sickness")
        fe3d:print(eventMessage)

/// Kill worker
IF workerIsDead IS <true>
    PUSH _availableWorkerNames _workerNames[_workerEventIndex]
    PULL _workerNames _workerEventIndex
    PULL _workerHealths _workerEventIndex
    PULL _workerBuildingIds _workerEventIndex
    ADD _totalDeadWorkers 1

/// Update event index
INT totalWorkers = misc:list_size("_workerNames")
IF workerIsDead IS <false>
    ADD _workerEventIndex 1
IF _workerEventIndex IS totalWorkers
    EDIT _workerEventIndex = 0
    EDIT _workerEventTimespan = _workerEventsTimespan
    DIV _workerEventTimespan totalWorkers

40 46
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "apiarist" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("apiarist_worker", <false>)
    fe3d:quad2d_set_visible("apiarist_time", <false>)
    fe3d:quad2d_set_visible("apiarist_convert", <false>)
    fe3d:quad2d_set_visible("apiarist_honey", <false>)
    fe3d:text2d_set_visible("apiarist_worker", <false>)
    fe3d:text2d_set_visible("apiarist_time", <false>)
    fe3d:text2d_set_visible("apiarist_honey", <false>)

/// Show GUI
IF _selectedBuildingType IS "apiarist"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("apiarist_time", <true>)
        fe3d:quad2d_set_visible("apiarist_convert", <true>)
        fe3d:quad2d_set_visible("apiarist_honey", <true>)
        fe3d:text2d_set_visible("apiarist_time", <true>)
        fe3d:text2d_set_visible("apiarist_honey", <true>)
    ELSE
        fe3d:quad2d_set_visible("apiarist_worker", <true>)
        fe3d:text2d_set_visible("apiarist_worker", <true>)

/// Update apiarists
DEF INT maxIndex = misc:list_size("_apiaristIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _apiaristIds[index])
    IF hasWorker IS <true>
        IF _apiaristTimers[index] IS _apiaristInterval
            IF _totalHoney LESS _maxHoney
                ADD _totalHoney 1
                ALT _apiaristTimers[index] = 0
        ELSE
            ADD _apiaristTimers[index] 1
    ADD index 1

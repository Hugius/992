0 0
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "nightman" OR _firstUpdate IS <true>
    fe3d:quad2d_set_visible("nightman_worker", <false>)
    fe3d:quad2d_set_visible("nightman_tool", <false>)
    fe3d:quad2d_set_visible("nightman_info", <false>)
    fe3d:text2d_set_visible("nightman_worker", <false>)
    fe3d:text2d_set_visible("nightman_tool", <false>)
    fe3d:text2d_set_visible("nightman_info", <false>)

/// Show GUI
IF _selectedBuildingType IS "nightman"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasTool IS <true>
            fe3d:quad2d_set_visible("nightman_info", <true>)
            fe3d:text2d_set_visible("nightman_info", <true>)
        ELSE
            fe3d:quad2d_set_visible("nightman_tool", <true>)
            fe3d:text2d_set_visible("nightman_tool", <true>)
            IF _totalTools MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalTools 1
                    PUSH _toolBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("nightman_worker", <true>)
        fe3d:text2d_set_visible("nightman_worker", <true>)

/// Update nightmen
DEF INT maxNightmanIndex = misc:list_size("_nightmanIds")
DEF INT nightmanIndex = 0
DEF BOL decreaseSicknessChance = <false>
LOOP
    IF nightmanIndex IS maxNightmanIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _nightmanIds[nightmanIndex])
    ALT hasTool = misc:list_contains("_toolBuildingIds", _nightmanIds[nightmanIndex])
    IF hasWorker IS <true> AND hasTool IS <true>
        ALT decreaseSicknessChance = <true>
        BREAK
    ADD nightmanIndex 1

IF decreaseSicknessChance IS <true>
    SUB _sicknessCatchChance 5

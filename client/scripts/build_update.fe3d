59 39
META script_type_update
META script_state_wait

fe3d:model_set_visible(_buildingNames[_buildingIndex], <false>)

IF _buildMode IS <false>
    EXIT

INT buildingCount = misc:list_size("_buildingNames")
BOL wheelUp = fe3d:input_is_mouse_scrolled("WHEEL_FORWARD")
BOL wheelDown = fe3d:input_is_mouse_scrolled("WHEEL_BACKWARD")

IF wheelUp IS <true>
    ADD _buildingIndex 1
    IF _buildingIndex IS buildingCount
        EDIT _buildingIndex = 0

IF wheelDown IS <true>
    IF _buildingIndex IS 0
        EDIT _buildingIndex = buildingCount
    SUB _buildingIndex 1

LST color = {0.0,0.5,0.0}
INT planksPrice = _buildingPricesPlanks[_buildingIndex]
INT stonePrice = _buildingPricesStone[_buildingIndex]
INT clayPrice = _buildingPricesClay[_buildingIndex]
INT strawPrice = _buildingPricesStraw[_buildingIndex]
INT ironPrice = _buildingPricesIron[_buildingIndex]

IF _totalPlanks LESS planksPrice
    EDIT color = {0.5,0.0,0.0}
IF _totalStone LESS stonePrice
    EDIT color = {0.5,0.0,0.0}
IF _totalClay LESS clayPrice
    EDIT color = {0.5,0.0,0.0}
IF _totalStraw LESS strawPrice
    EDIT color = {0.5,0.0,0.0}
IF _totalIron LESS ironPrice
    EDIT color = {0.5,0.0,0.0}

STR buildingName = _buildingNames[_buildingIndex]
LST partIds = fe3d:model_get_part_ids(buildingName)
DEC x = fe3d:raycast_get_point_on_terrain_x()
DEC y = fe3d:raycast_get_point_on_terrain_y()
DEC z = fe3d:raycast_get_point_on_terrain_z()
INT partCount = misc:list_size("partIds")
INT index = 0

fe3d:model_set_base_position(buildingName, x, y, z)
fe3d:model_set_visible(buildingName, <true>)

LOOP
    IF index IS partCount
        BREAK
    fe3d:model_set_color(buildingName, partIds[index], color[0], color[1], color[2])
    ADD index 1

BOL clicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")

IF clicked IS <true> AND color[1] NOT 0.0
    CAST _buildingCounter STR
    STR newBuildingId = misc:string_concat(buildingName, _buildingCounter)
    CAST _buildingCounter INT
    ADD _buildingCounter 1
    fe3d:model_place(newBuildingId, buildingName, 0.0, 0.0, 0.0)
    fe3d:model_set_base_position(newBuildingId, x, y, z)
    SUB _totalPlanks planksPrice
    SUB _totalStone stonePrice
    SUB _totalClay clayPrice
    SUB _totalStraw strawPrice
    SUB _totalIron ironPrice

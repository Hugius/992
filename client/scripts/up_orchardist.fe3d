21 61
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "orchardist" OR _firstUpdate IS <true>
    fe3d:quad2d_set_visible("orchardist_worker", <false>)
    fe3d:quad2d_set_visible("orchardist_time", <false>)
    fe3d:quad2d_set_visible("orchardist_convert", <false>)
    fe3d:quad2d_set_visible("orchardist_fruit", <false>)
    fe3d:text2d_set_visible("orchardist_worker", <false>)
    fe3d:text2d_set_visible("orchardist_time", <false>)
    fe3d:text2d_set_visible("orchardist_fruit", <false>)

/// Show GUI
IF _selectedBuildingType IS "orchardist"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("orchardist_time", <true>)
        fe3d:quad2d_set_visible("orchardist_convert", <true>)
        fe3d:quad2d_set_visible("orchardist_fruit", <true>)
        fe3d:text2d_set_visible("orchardist_time", <true>)
        fe3d:text2d_set_visible("orchardist_fruit", <true>)
    ELSE
        fe3d:quad2d_set_visible("orchardist_worker", <true>)
        fe3d:text2d_set_visible("orchardist_worker", <true>)

/// Update orchardists
DEF STR treeId = ""
DEF DEC growth = 0.0
DEF INT maxIndex = misc:list_size("_orchardistIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _orchardistIds[index])
    IF hasWorker IS <true>
        IF _orchardistGrowths[index] IS 0.0
            ADD _orchardistGrowths[index] _minOrchardistGrowth
        ELIF _orchardistGrowths[index] MORE _maxOrchardistGrowth
            IF _orchardistTimers[index] IS _orchardistInterval
                ALT _orchardistTimers[index] = 0
                ADD _totalFruit 1
            ELSE
                ADD _orchardistTimers[index] 1
    IF _orchardistGrowths[index] MORE 0.0 AND _orchardistGrowths[index] LESS _maxOrchardistGrowth
        DEF INT interval = _orchardistInterval
        DEF DEC speed = _maxOrchardistGrowth
        CAST interval DEC
        DIV speed interval
        ADD _orchardistGrowths[index] speed
    ALT growth = _orchardistGrowths[index]
    ALT treeId = misc:string_concat(_orchardistIds[index], "_1")
    fe3d:model_set_base_size(treeId, growth, growth, growth)
    ALT treeId = misc:string_concat(_orchardistIds[index], "_2")
    fe3d:model_set_base_size(treeId, growth, growth, growth)
    ALT treeId = misc:string_concat(_orchardistIds[index], "_3")
    fe3d:model_set_base_size(treeId, growth, growth, growth)
    ALT treeId = misc:string_concat(_orchardistIds[index], "_4")
    fe3d:model_set_base_size(treeId, growth, growth, growth)
    ADD index 1

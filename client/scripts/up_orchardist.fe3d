54 18
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "orchardist" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("orchardist_worker", <false>)
    fe3d:quad2d_set_visible("orchardist_time", <false>)
    fe3d:quad2d_set_visible("orchardist_convert", <false>)
    fe3d:quad2d_set_visible("orchardist_fruit", <false>)
    fe3d:text2d_set_visible("orchardist_worker", <false>)
    fe3d:text2d_set_visible("orchardist_time", <false>)
    fe3d:text2d_set_visible("orchardist_fruit", <false>)

/// Show GUI
IF _selectedBuildingType IS "orchardist"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("orchardist_time", <true>)
        fe3d:quad2d_set_visible("orchardist_convert", <true>)
        fe3d:quad2d_set_visible("orchardist_fruit", <true>)
        fe3d:text2d_set_visible("orchardist_time", <true>)
        fe3d:text2d_set_visible("orchardist_fruit", <true>)
    ELSE
        fe3d:quad2d_set_visible("orchardist_worker", <true>)
        fe3d:text2d_set_visible("orchardist_worker", <true>)

/// Update orchardists
DEF STR treeId = ""
DEF DEC growth = 0.0
DEF INT maxIndex = misc:list_size("_orchardistIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    IF _orchardistGrowths[index] LESS 0.25
        ADD _orchardistGrowths[index] 0.0001
        ALT growth = _orchardistGrowths[index]
        ALT treeId = misc:string_concat(_orchardistIds[index], "_1")
        fe3d:model_set_base_size(treeId, growth, growth, growth)
        ALT treeId = misc:string_concat(_orchardistIds[index], "_2")
        fe3d:model_set_base_size(treeId, growth, growth, growth)
        ALT treeId = misc:string_concat(_orchardistIds[index], "_3")
        fe3d:model_set_base_size(treeId, growth, growth, growth)
        ALT treeId = misc:string_concat(_orchardistIds[index], "_4")
        fe3d:model_set_base_size(treeId, growth, growth, growth)
    ELSE
        ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _orchardistIds[index])
        IF hasWorker IS <true>
            IF _orchardistTimers[index] IS _orchardistInterval
                IF _totalFruit LESS _maxFruit
                    ADD _totalFruit 1
                    ALT _orchardistTimers[index] = 0
            ELSE
                ADD _orchardistTimers[index] 1
    ADD index 1

58 31
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "stonecutter" OR _firstUpdate IS <true>
    fe3d:quad2d_set_visible("stonecutter_worker", <false>)
    fe3d:quad2d_set_visible("stonecutter_tool", <false>)
    fe3d:quad2d_set_visible("stonecutter_stone", <false>)
    fe3d:quad2d_set_visible("stonecutter_convert", <false>)
    fe3d:quad2d_set_visible("stonecutter_bricks", <false>)
    fe3d:text2d_set_visible("stonecutter_worker", <false>)
    fe3d:text2d_set_visible("stonecutter_tool", <false>)
    fe3d:text2d_set_visible("stonecutter_stone", <false>)
    fe3d:text2d_set_visible("stonecutter_bricks", <false>)

/// Show GUI
IF _selectedBuildingType IS "stonecutter"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasTool IS <true>
            fe3d:quad2d_set_visible("stonecutter_stone", <true>)
            fe3d:quad2d_set_visible("stonecutter_convert", <true>)
            fe3d:quad2d_set_visible("stonecutter_bricks", <true>)
            fe3d:text2d_set_visible("stonecutter_stone", <true>)
            fe3d:text2d_set_visible("stonecutter_bricks", <true>)
        ELSE
            fe3d:quad2d_set_visible("stonecutter_tool", <true>)
            fe3d:text2d_set_visible("stonecutter_tool", <true>)
            IF _totalTools MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalTools 1
                    PUSH _toolBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("stonecutter_worker", <true>)
        fe3d:text2d_set_visible("stonecutter_worker", <true>)

/// Update stonecutters
DEF INT maxIndex = misc:list_size("_stonecutterIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _stonecutterIds[index])
    ALT hasTool = misc:list_contains("_toolBuildingIds", _stonecutterIds[index])
    IF hasWorker IS <true> AND hasTool IS <true>
        IF _stonecutterTimers[index] IS 0
            IF _totalStone MORE 0
                SUB _totalStone 1
                ADD _stonecutterTimers[index] 1
        ELIF _stonecutterTimers[index] IS _stonecutterInterval
            ALT _stonecutterTimers[index] = 0
            ADD _totalBricks 15
        ELSE
            ADD _stonecutterTimers[index] 1
    ADD index 1

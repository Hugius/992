77 35
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasBow = <false>
DEF BOL hasArrow = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "hunter" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("hunter_worker", <false>)
    fe3d:quad2d_set_visible("hunter_bow", <false>)
    fe3d:quad2d_set_visible("hunter_arrows", <false>)
    fe3d:quad2d_set_visible("hunter_time", <false>)
    fe3d:quad2d_set_visible("hunter_convert", <false>)
    fe3d:quad2d_set_visible("hunter_rawmeat", <false>)
    fe3d:text2d_set_visible("hunter_worker", <false>)
    fe3d:text2d_set_visible("hunter_bow", <false>)
    fe3d:text2d_set_visible("hunter_arrows", <false>)
    fe3d:text2d_set_visible("hunter_time", <false>)
    fe3d:text2d_set_visible("hunter_rawmeat", <false>)

/// Show GUI
IF _selectedBuildingType IS "hunter"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasBow = misc:list_contains("_bowBuildingIds", _selectedBuildingId)
    ALT hasArrow = misc:list_contains("_arrowBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasBow IS <true>
            IF hasArrow IS <true>
                fe3d:quad2d_set_visible("hunter_time", <true>)
                fe3d:quad2d_set_visible("hunter_convert", <true>)
                fe3d:quad2d_set_visible("hunter_rawmeat", <true>)
                fe3d:text2d_set_visible("hunter_time", <true>)
                fe3d:text2d_set_visible("hunter_rawmeat", <true>)
            ELSE
                fe3d:quad2d_set_visible("hunter_arrows", <true>)
                fe3d:text2d_set_visible("hunter_arrows", <true>)
                IF _totalArrows MORE 4
                    ALT _cursorType = "HAND"
                    DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                    IF isLmbClicked IS <true>
                        SUB _totalArrows 5
                        PUSH _arrowBuildingIds _selectedBuildingId
                        PUSH _arrowBuildingIds _selectedBuildingId
                        PUSH _arrowBuildingIds _selectedBuildingId
                        PUSH _arrowBuildingIds _selectedBuildingId
                        PUSH _arrowBuildingIds _selectedBuildingId
        ELSE
            fe3d:quad2d_set_visible("hunter_bow", <true>)
            fe3d:text2d_set_visible("hunter_bow", <true>)
            IF _totalBows MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalBows 1
                    PUSH _bowBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("hunter_worker", <true>)
        fe3d:text2d_set_visible("hunter_worker", <true>)

/// Update hunters
DEF INT maxIndex = misc:list_size("_hunterIds")
DEF INT index = 0
DEF INT arrowIndex = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _hunterIds[index])
    ALT hasBow = misc:list_contains("_bowBuildingIds", _hunterIds[index])
    ALT hasArrow = misc:list_contains("_arrowBuildingIds", _hunterIds[index])
    IF hasWorker IS <true> AND hasBow IS <true> AND hasArrow IS <true>
        IF _hunterTimers[index] IS _hunterInterval
            ALT _hunterTimers[index] = 0
            ALT arrowIndex = misc:list_index("_arrowBuildingIds", _hunterIds[index])
            PULL _arrowBuildingIds arrowIndex
            IF _totalRawMeat LESS _maxRawMeat
                ADD _totalRawMeat 1
        ELSE
            ADD _hunterTimers[index] 1
    ADD index 1

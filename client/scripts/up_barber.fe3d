39 17
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "barber" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("barber_worker", <false>)
    fe3d:quad2d_set_visible("barber_tool", <false>)
    fe3d:quad2d_set_visible("barber_info", <false>)
    fe3d:text2d_set_visible("barber_worker", <false>)
    fe3d:text2d_set_visible("barber_tool", <false>)
    fe3d:text2d_set_visible("barber_info", <false>)

/// Show GUI
IF _selectedBuildingType IS "barber"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasTool IS <true>
            fe3d:quad2d_set_visible("barber_info", <true>)
            fe3d:text2d_set_visible("barber_info", <true>)
        ELSE
            fe3d:quad2d_set_visible("barber_tool", <true>)
            fe3d:text2d_set_visible("barber_tool", <true>)
            IF _totalTools MORE 0
                ALT _cursorType = "HAND"
                DEF BOL lmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF lmbClicked IS <true>
                    SUB _totalTools 1
                    PUSH _toolBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("barber_worker", <true>)
        fe3d:text2d_set_visible("barber_worker", <true>)

/// Update barbers
DEF INT maxIndex = misc:list_size("_barberIds")
DEF INT index = 0
DEF BOL mustDecreaseSicknessChance = <false>
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _barberIds[index])
    ALT hasTool = misc:list_contains("_toolBuildingIds", _barberIds[index])
    IF hasWorker IS <true> AND hasTool IS <true>
        ALT mustDecreaseSicknessChance = <true>
        BREAK
    ADD index 1

IF mustDecreaseSicknessChance IS <true>
    SUB _sicknessCatchChance 5

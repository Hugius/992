60 41
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "smoker" OR _firstUpdate IS <true>
    fe3d:quad2d_set_visible("smoker_worker", <false>)
    fe3d:quad2d_set_visible("smoker_rawmeat", <false>)
    fe3d:quad2d_set_visible("smoker_rawfish", <false>)
    fe3d:quad2d_set_visible("smoker_convert", <false>)
    fe3d:quad2d_set_visible("smoker_smokedmeat", <false>)
    fe3d:quad2d_set_visible("smoker_smokedfish", <false>)
    fe3d:text2d_set_visible("smoker_worker", <false>)
    fe3d:text2d_set_visible("smoker_rawmeat", <false>)
    fe3d:text2d_set_visible("smoker_rawfish", <false>)
    fe3d:text2d_set_visible("smoker_smokedmeat", <false>)
    fe3d:text2d_set_visible("smoker_smokedfish", <false>)

/// Show GUI
IF _selectedBuildingType IS "smoker"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("smoker_rawmeat", <true>)
        fe3d:quad2d_set_visible("smoker_rawfish", <true>)
        fe3d:quad2d_set_visible("smoker_convert", <true>)
        fe3d:quad2d_set_visible("smoker_smokedmeat", <true>)
        fe3d:quad2d_set_visible("smoker_smokedfish", <true>)
        fe3d:text2d_set_visible("smoker_rawmeat", <true>)
        fe3d:text2d_set_visible("smoker_rawfish", <true>)
        fe3d:text2d_set_visible("smoker_smokedmeat", <true>)
        fe3d:text2d_set_visible("smoker_smokedfish", <true>)
    ELSE
        fe3d:quad2d_set_visible("smoker_worker", <true>)
        fe3d:text2d_set_visible("smoker_worker", <true>)

/// Update smokers
DEF INT maxIndex = misc:list_size("_smokerIds")
DEF INT index = 0
DEF INT randomNumber = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _smokerIds[index])
    IF hasWorker IS <true>
        IF _smokerTimers[index] IS 0
            ALT randomNumber = math:random_integer(1, 2)
            IF randomNumber IS 1 AND _totalRawMeat MORE 0
                SUB _totalRawMeat 1
                ADD _smokerTimers[index] 1
                ALT _smokerTurns[index] = 1
            ELIF randomNumber IS 2 AND _totalRawFish MORE 0
                SUB _totalRawFish 1
                ADD _smokerTimers[index] 1
                ALT _smokerTurns[index] = 2
        ELIF _smokerTimers[index] IS _smokerInterval
            ALT _smokerTimers[index] = 0
            IF _smokerTurns[index] IS 1
                ADD _totalSmokedMeat 1
            ELIF _smokerTurns[index] IS 2
                ADD _totalSmokedFish 1
        ELSE
            ADD _smokerTimers[index] 1
    ADD index 1

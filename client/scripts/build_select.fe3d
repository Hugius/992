17 0
META script_type_update
META script_state_wait

/// Update raycasting
fe3d:raycast_calculate_aabbs_intersection()
STR buildingId = fe3d:raycast_get_closest_model("")

/// Prepare values
BOL hovered = <false>

/// Extract building type
IF buildingId NOT "" AND _buildMode IS <false>
    LST splitPlacedId = misc:string_split(buildingId, "_")
    STR buildingType = splitPlacedId[0]
    BOL contains = misc:list_contains("_buildingTypes", buildingType)
    IF contains IS <true>
        EDIT hovered = <true>

/// Update building type GUI
IF hovered IS <true>
    fe3d:quad2d_set_visible("building_type", <true>)
    fe3d:text2d_set_visible("building_type", <true>)
    STR type = misc:string_capitalize(buildingType)
    INT width = misc:string_size(type)
    CAST width DEC
    MUL width 0.02
    fe3d:text2d_set_content("building_type", type)
    fe3d:text2d_set_size("building_type", width, 0.1)
    EDIT _selectedBuildingId = buildingId
    EDIT _selectedBuildingType = buildingType
ELSE
    fe3d:quad2d_set_visible("building_type", <false>)
    fe3d:text2d_set_visible("building_type", <false>)
    EDIT _selectedBuildingId = ""
    EDIT _selectedBuildingType = ""

/// Update building menu GUI
BOL selected = (_selectedBuildingType NOT "")
fe3d:quad2d_set_visible("building_menu", selected)
fe3d:text2d_set_visible("building_menu", selected)

/// Update cursor type
fe3d:cursor_set_type(_cursorType)
EDIT _cursorType = "ARROW"

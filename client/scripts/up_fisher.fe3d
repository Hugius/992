38 12
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "fisher" OR _firstUpdate IS <true>
    fe3d:quad2d_set_visible("fisher_worker", <false>)
    fe3d:quad2d_set_visible("fisher_time", <false>)
    fe3d:quad2d_set_visible("fisher_convert", <false>)
    fe3d:quad2d_set_visible("fisher_rawfish", <false>)
    fe3d:text2d_set_visible("fisher_time", <false>)
    fe3d:text2d_set_visible("fisher_worker", <false>)
    fe3d:text2d_set_visible("fisher_rawfish", <false>)

/// Show GUI
IF _selectedBuildingType IS "fisher"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("fisher_time", <true>)
        fe3d:quad2d_set_visible("fisher_convert", <true>)
        fe3d:quad2d_set_visible("fisher_rawfish", <true>)
        fe3d:text2d_set_visible("fisher_time", <true>)
        fe3d:text2d_set_visible("fisher_rawfish", <true>)
    ELSE
        fe3d:quad2d_set_visible("fisher_worker", <true>)
        fe3d:text2d_set_visible("fisher_worker", <true>)

/// Update fishers
DEF INT maxIndex = misc:list_size("_fisherIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _fisherIds[index])
    IF hasWorker IS <true>
        IF _fisherTimers[index] IS _fisherInterval
            ALT _fisherTimers[index] = 0
            IF _totalRawFish LESS _maxRawFish
                ADD _totalRawFish 1
        ELSE
            ADD _fisherTimers[index] 1
    ADD index 1

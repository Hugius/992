59 18
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "tanner" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("tanner_worker", <false>)
    fe3d:quad2d_set_visible("tanner_tool", <false>)
    fe3d:quad2d_set_visible("tanner_hides", <false>)
    fe3d:quad2d_set_visible("tanner_convert", <false>)
    fe3d:quad2d_set_visible("tanner_leather", <false>)
    fe3d:text2d_set_visible("tanner_worker", <false>)
    fe3d:text2d_set_visible("tanner_tool", <false>)
    fe3d:text2d_set_visible("tanner_hides", <false>)
    fe3d:text2d_set_visible("tanner_leather", <false>)

/// Show GUI
IF _selectedBuildingType IS "tanner"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasTool IS <true>
            fe3d:quad2d_set_visible("tanner_hides", <true>)
            fe3d:quad2d_set_visible("tanner_convert", <true>)
            fe3d:quad2d_set_visible("tanner_leather", <true>)
            fe3d:text2d_set_visible("tanner_hides", <true>)
            fe3d:text2d_set_visible("tanner_leather", <true>)
        ELSE
            fe3d:quad2d_set_visible("tanner_tool", <true>)
            fe3d:text2d_set_visible("tanner_tool", <true>)
            IF _totalTools MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalTools 1
                    PUSH _toolBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("tanner_worker", <true>)
        fe3d:text2d_set_visible("tanner_worker", <true>)

/// Update tanners
DEF INT maxIndex = misc:list_size("_tannerIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _tannerIds[index])
    ALT hasTool = misc:list_contains("_toolBuildingIds", _tannerIds[index])
    IF hasWorker IS <true> AND hasTool IS <true>
        IF _tannerTimers[index] IS _tannerInterval
            IF _totalHides MORE 0
                ADD _totalLeather 1
                IF _totalLeather MORE _maxLeather
                    SUB _totalLeather 1
                ELSE
                    SUB _totalHides 1
                    ALT _tannerTimers[index] = 0
        ELSE
            ADD _tannerTimers[index] 1
    ADD index 1

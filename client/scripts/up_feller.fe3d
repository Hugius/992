45 21
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "feller" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("feller_worker", <false>)
    fe3d:quad2d_set_visible("feller_tool", <false>)
    fe3d:quad2d_set_visible("feller_time", <false>)
    fe3d:quad2d_set_visible("feller_convert", <false>)
    fe3d:quad2d_set_visible("feller_lumber", <false>)
    fe3d:text2d_set_visible("feller_worker", <false>)
    fe3d:text2d_set_visible("feller_tool", <false>)
    fe3d:text2d_set_visible("feller_time", <false>)
    fe3d:text2d_set_visible("feller_lumber", <false>)

/// Show GUI
IF _selectedBuildingType IS "feller"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasTool IS <true>
            fe3d:quad2d_set_visible("feller_time", <true>)
            fe3d:quad2d_set_visible("feller_convert", <true>)
            fe3d:quad2d_set_visible("feller_lumber", <true>)
            fe3d:text2d_set_visible("feller_time", <true>)
            fe3d:text2d_set_visible("feller_lumber", <true>)
        ELSE
            fe3d:quad2d_set_visible("feller_tool", <true>)
            fe3d:text2d_set_visible("feller_tool", <true>)
            IF _totalTools MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalTools 1
                    PUSH _toolBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("feller_worker", <true>)
        fe3d:text2d_set_visible("feller_worker", <true>)

/// Update fellers
DEF LST treeIds = {}
DEF INT treeCount = 0
DEF INT treeIndex = 0
DEF INT maxIndex = misc:list_size("_fellerIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _fellerIds[index])
    ALT hasTool = misc:list_contains("_toolBuildingIds", _fellerIds[index])
    IF hasWorker IS <true> AND hasTool IS <true>
        IF _fellerTimers[index] IS _fellerInterval
            ALT _fellerTimers[index] = 0
            IF _totalLumber LESS _maxLumber
                ALT treeIds = fe3d:model_find_ids("tree")
                ALT treeCount = misc:list_size("treeIds")
                ALT treeIndex = math:random_integer(1, treeCount)
                SUB treeIndex 1
                fe3d:model_delete(treeIds[treeIndex])
                ADD _totalLumber 1
        ELSE
            ADD _fellerTimers[index] 1
    ADD index 1

43 12
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "miller" OR _firstUpdate IS <true>
    fe3d:quad2d_set_visible("miller_worker", <false>)
    fe3d:quad2d_set_visible("miller_grain", <false>)
    fe3d:quad2d_set_visible("miller_convert", <false>)
    fe3d:quad2d_set_visible("miller_flour", <false>)
    fe3d:text2d_set_visible("miller_worker", <false>)
    fe3d:text2d_set_visible("miller_grain", <false>)
    fe3d:text2d_set_visible("miller_flour", <false>)

/// Show GUI
IF _selectedBuildingType IS "miller"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("miller_grain", <true>)
        fe3d:quad2d_set_visible("miller_convert", <true>)
        fe3d:quad2d_set_visible("miller_flour", <true>)
        fe3d:text2d_set_visible("miller_grain", <true>)
        fe3d:text2d_set_visible("miller_flour", <true>)
    ELSE
        fe3d:quad2d_set_visible("miller_worker", <true>)
        fe3d:text2d_set_visible("miller_worker", <true>)

/// Update millers
DEF INT maxIndex = misc:list_size("_millerIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _millerIds[index])
    IF hasWorker IS <true>
        IF _millerTimers[index] IS 0
            IF _totalGrain MORE 0
                SUB _totalGrain 1
                ADD _millerTimers[index] 1
        ELIF _millerTimers[index] IS _millerInterval
            ALT _millerTimers[index] = 0
            ADD _totalFlour 1
        ELSE
            ADD _millerTimers[index] 1
    ADD index 1

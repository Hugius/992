49 18
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "brewer" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("brewer_worker", <false>)
    fe3d:quad2d_set_visible("brewer_grain", <false>)
    fe3d:quad2d_set_visible("brewer_firewood", <false>)
    fe3d:quad2d_set_visible("brewer_convert", <false>)
    fe3d:quad2d_set_visible("brewer_ale", <false>)
    fe3d:text2d_set_visible("brewer_worker", <false>)
    fe3d:text2d_set_visible("brewer_grain", <false>)
    fe3d:text2d_set_visible("brewer_firewood", <false>)
    fe3d:text2d_set_visible("brewer_ale", <false>)

/// Show GUI
IF _selectedBuildingType IS "brewer"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("brewer_grain", <true>)
        fe3d:quad2d_set_visible("brewer_firewood", <true>)
        fe3d:quad2d_set_visible("brewer_convert", <true>)
        fe3d:quad2d_set_visible("brewer_ale", <true>)
        fe3d:text2d_set_visible("brewer_grain", <true>)
        fe3d:text2d_set_visible("brewer_firewood", <true>)
        fe3d:text2d_set_visible("brewer_ale", <true>)
    ELSE
        fe3d:quad2d_set_visible("brewer_worker", <true>)
        fe3d:text2d_set_visible("brewer_worker", <true>)

/// Update brewers
DEF INT maxIndex = misc:list_size("_brewerIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _brewerIds[index])
    IF hasWorker IS <true>
        IF _brewerTimers[index] IS _brewerInterval
            IF _totalGrain MORE 0 AND _totalFirewood MORE 0
                ADD _totalAle 10
                IF _totalAle MORE _maxAle
                    SUB _totalAle 10
                ELSE
                    SUB _totalGrain 1
                    SUB _totalFirewood 1
                    ALT _brewerTimers[index] = 0
        ELSE
            ADD _brewerTimers[index] 1
    ADD index 1

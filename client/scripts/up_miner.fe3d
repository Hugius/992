55 31
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "miner" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("miner_worker", <false>)
    fe3d:quad2d_set_visible("miner_tool", <false>)
    fe3d:quad2d_set_visible("miner_time", <false>)
    fe3d:quad2d_set_visible("miner_convert", <false>)
    fe3d:quad2d_set_visible("miner_ore", <false>)
    fe3d:text2d_set_visible("miner_worker", <false>)
    fe3d:text2d_set_visible("miner_tool", <false>)
    fe3d:text2d_set_visible("miner_time", <false>)
    fe3d:text2d_set_visible("miner_ore", <false>)

/// Show GUI
IF _selectedBuildingType IS "miner"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasTool IS <true>
            fe3d:quad2d_set_visible("miner_time", <true>)
            fe3d:quad2d_set_visible("miner_convert", <true>)
            fe3d:quad2d_set_visible("miner_ore", <true>)
            fe3d:text2d_set_visible("miner_time", <true>)
            fe3d:text2d_set_visible("miner_ore", <true>)
        ELSE
            fe3d:quad2d_set_visible("miner_tool", <true>)
            fe3d:text2d_set_visible("miner_tool", <true>)
            IF _totalTools MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalTools 1
                    PUSH _toolBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("miner_worker", <true>)
        fe3d:text2d_set_visible("miner_worker", <true>)

/// Update miners
DEF INT maxIndex = misc:list_size("_minerIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _minerIds[index])
    ALT hasTool = misc:list_contains("_toolBuildingIds", _minerIds[index])
    IF hasWorker IS <true> AND hasTool IS <true>
        IF _minerTimers[index] IS _minerInterval
            ALT _minerTimers[index] = 0
            IF _totalOre LESS _maxOre
                ADD _totalOre 1
        ELSE
            ADD _minerTimers[index] 1
    ADD index 1

0 0
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "fletcher" OR _firstUpdate IS <true>
    fe3d:quad2d_set_visible("fletcher_worker", <false>)
    fe3d:quad2d_set_visible("fletcher_tool", <false>)
    fe3d:quad2d_set_visible("fletcher_iron", <false>)
    fe3d:quad2d_set_visible("fletcher_planks", <false>)
    fe3d:quad2d_set_visible("fletcher_feathers", <false>)
    fe3d:quad2d_set_visible("fletcher_convert", <false>)
    fe3d:quad2d_set_visible("fletcher_arrows", <false>)
    fe3d:text2d_set_visible("fletcher_worker", <false>)
    fe3d:text2d_set_visible("fletcher_tool", <false>)
    fe3d:text2d_set_visible("fletcher_iron", <false>)
    fe3d:text2d_set_visible("fletcher_planks", <false>)
    fe3d:text2d_set_visible("fletcher_feathers", <false>)
    fe3d:text2d_set_visible("fletcher_arrows", <false>)

/// Show GUI
IF _selectedBuildingType IS "fletcher"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasTool IS <true>
            fe3d:quad2d_set_visible("fletcher_iron", <true>)
            fe3d:quad2d_set_visible("fletcher_planks", <true>)
            fe3d:quad2d_set_visible("fletcher_feathers", <true>)
            fe3d:quad2d_set_visible("fletcher_convert", <true>)
            fe3d:quad2d_set_visible("fletcher_arrows", <true>)
            fe3d:text2d_set_visible("fletcher_iron", <true>)
            fe3d:text2d_set_visible("fletcher_planks", <true>)
            fe3d:text2d_set_visible("fletcher_feathers", <true>)
            fe3d:text2d_set_visible("fletcher_arrows", <true>)
        ELSE
            fe3d:quad2d_set_visible("fletcher_tool", <true>)
            fe3d:text2d_set_visible("fletcher_tool", <true>)
            IF _totalTools MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalTools 1
                    PUSH _toolBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("fletcher_worker", <true>)
        fe3d:text2d_set_visible("fletcher_worker", <true>)

/// Update fletchers
DEF INT maxFletcherIndex = misc:list_size("_fletcherIds")
DEF INT fletcherIndex = 0
LOOP
    IF fletcherIndex IS maxFletcherIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _fletcherIds[fletcherIndex])
    ALT hasTool = misc:list_contains("_toolBuildingIds", _fletcherIds[fletcherIndex])
    IF hasWorker IS <true> AND hasTool IS <true>
        IF _fletcherTimers[fletcherIndex] IS 0
            IF _totalIron MORE 0 AND _totalPlanks MORE 0 AND _totalFeathers MORE 0
                SUB _totalIron 1
                SUB _totalPlanks 1
                SUB _totalFeathers 1
                ADD _fletcherTimers[fletcherIndex] 1
        ELIF _fletcherTimers[fletcherIndex] IS _fletcherInterval
            ALT _fletcherTimers[fletcherIndex] = 0
            ADD _totalArrows 10
        ELSE
            ADD _fletcherTimers[fletcherIndex] 1
    ADD fletcherIndex 1

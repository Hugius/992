20 0
META script_type_update
META script_state_waiting

/// Save last build mode
ALT _lastBuildMode = _buildMode

/// Check default buildings
DEF BOL carpenterHasTool = misc:list_contains("_toolBuildingIds", "carpenter_1")
DEF BOL carpenterHasWorker = misc:list_contains("_workerJobBuildingIds", "carpenter_1")
DEF BOL masonHasTool = misc:list_contains("_toolBuildingIds", "mason_2")
DEF BOL masonHasWorker = misc:list_contains("_workerJobBuildingIds", "mason_2")
DEF BOL rooferHasTool = misc:list_contains("_toolBuildingIds", "roofer_3")
DEF BOL rooferHasWorker = misc:list_contains("_workerJobBuildingIds", "roofer_3")

/// Check build button hoverability
DEF BOL isButtonHoverable = <false>
IF carpenterHasTool IS <true> AND carpenterHasWorker IS <true>
    IF masonHasTool IS <true> AND masonHasWorker IS <true>
        IF rooferHasTool IS <true> AND rooferHasWorker IS <true>
            ALT isButtonHoverable = <true>

/// Update build button hoverability
IF isButtonHoverable IS <true>
    fe3d:quad2d_set_opacity("build", 1.0)
ELSE
    fe3d:quad2d_set_opacity("build", 0.5)
    fe3d:model_set_visible(_buildingTypes[_buildingIndex], <false>)
    ALT _buildMode = <false>

/// Update build button hover and click
DEF BOL buttonHovered = fe3d:quad2d_is_hovered("build")
IF buttonHovered IS <true> AND isButtonHoverable IS <true>
    fe3d:quad2d_set_lightness("build", 1.25)
    ALT _cursorType = "HAND"
    DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
    IF isLmbClicked IS <true>
        IF _buildMode IS <true>
            fe3d:model_set_visible(_buildingTypes[_buildingIndex], <false>)
            ALT _buildMode = <false>
        ELSE
            fe3d:model_set_visible(_buildingTypes[_buildingIndex], <true>)
            ALT _buildMode = <true>
ELSE
    fe3d:quad2d_set_lightness("build", 1.0)

/// Update GUI
fe3d:quad2d_set_visible("building_price_type", _buildMode)
fe3d:quad2d_set_visible("building_price_planks", _buildMode)
fe3d:quad2d_set_visible("building_price_stone", _buildMode)
fe3d:quad2d_set_visible("building_price_clay", _buildMode)
fe3d:quad2d_set_visible("building_price_straw", _buildMode)
fe3d:text2d_set_visible("building_price_type", _buildMode)
fe3d:text2d_set_visible("building_price_planks", _buildMode)
fe3d:text2d_set_visible("building_price_stone", _buildMode)
fe3d:text2d_set_visible("building_price_clay", _buildMode)
fe3d:text2d_set_visible("building_price_straw", _buildMode)

/// Check if build mode enabled
IF _buildMode IS <false>
    EXIT

/// Update GUI
DEF STR textContent = misc:string_capitalize(_buildingTypes[_buildingIndex])
DEF INT textWidth = misc:string_size(textContent)
CAST textWidth DEC
MUL textWidth 0.03
fe3d:text2d_set_content("building_price_type", textContent)
fe3d:text2d_set_size("building_price_type", textWidth, 0.15)

/// Update GUI
DEF STR planksPriceText = misc:string_zeroed_integer(_buildingPricesPlanks[_buildingIndex], 4)
DEF STR stonePriceText = misc:string_zeroed_integer(_buildingPricesStone[_buildingIndex], 4)
DEF STR clayPriceText = misc:string_zeroed_integer(_buildingPricesClay[_buildingIndex], 4)
DEF STR strawPriceText = misc:string_zeroed_integer(_buildingPricesStraw[_buildingIndex], 4)
fe3d:text2d_set_content("building_price_planks", planksPriceText)
fe3d:text2d_set_content("building_price_stone", stonePriceText)
fe3d:text2d_set_content("building_price_clay", clayPriceText)
fe3d:text2d_set_content("building_price_straw", strawPriceText)

/// Hide last building
fe3d:model_set_visible(_buildingTypes[_buildingIndex], <false>)

/// Update scroll wheel
DEF INT buildingCount = misc:list_size("_buildingTypes")
DEF BOL scrolledUp = fe3d:input_is_mouse_scrolled("WHEEL_FORWARD")
DEF BOL scrollDown = fe3d:input_is_mouse_scrolled("WHEEL_BACKWARD")
IF scrolledUp IS <true>
    ADD _buildingIndex 1
    IF _buildingIndex IS buildingCount
        ALT _buildingIndex = 0
IF scrollDown IS <true>
    IF _buildingIndex IS 0
        ALT _buildingIndex = buildingCount
    SUB _buildingIndex 1

/// Show current building
fe3d:model_set_visible(_buildingTypes[_buildingIndex], <true>)

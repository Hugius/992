0 0
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "roper" OR _firstUpdate IS <true>
  fe3d:quad2d_set_visible("roper_worker", <false>)
  fe3d:quad2d_set_visible("roper_straw", <false>)
  fe3d:quad2d_set_visible("roper_convert", <false>)
  fe3d:quad2d_set_visible("roper_fishingnet", <false>)
  fe3d:text2d_set_visible("roper_worker", <false>)
  fe3d:text2d_set_visible("roper_straw", <false>)
  fe3d:text2d_set_visible("roper_fishingnet", <false>)

/// Show GUI
IF _selectedBuildingType IS "roper"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    fe3d:quad2d_set_visible("roper_straw", <true>)
    fe3d:quad2d_set_visible("roper_convert", <true>)
    fe3d:quad2d_set_visible("roper_fishingnet", <true>)
    fe3d:text2d_set_visible("roper_straw", <true>)
    fe3d:text2d_set_visible("roper_fishingnet", <true>)
  ELSE
    fe3d:quad2d_set_visible("roper_worker", <true>)
    fe3d:text2d_set_visible("roper_worker", <true>)

/// Update ropers
DEF INT maxRoperIndex = misc:list_size("_roperIds")
DEF INT roperIndex = 0
LOOP
  IF roperIndex IS maxRoperIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _roperIds[roperIndex])
  IF hasWorker IS <true>
    IF _roperTimers[roperIndex] IS 0
      IF _totalStraw MORE 0
        SUB _totalStraw 1
        ADD _roperTimers[roperIndex] 1
    ELIF _roperTimers[roperIndex] IS _roperInterval
      ALT _roperTimers[roperIndex] = 0
      ADD _totalFishingNets 1
    ELSE
      ADD _roperTimers[roperIndex] 1
  ADD roperIndex 1

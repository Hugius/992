6 55
META script_type_update
META script_state_wait

IF _buildMode IS <false>
    EXIT

fe3d:raycast_calculate_terrain_intersection(100.0, 0.1)

STR buildingName = _buildingNames[_buildingIndex]
INT planksPrice = _buildingPricesPlanks[_buildingIndex]
INT stonePrice = _buildingPricesStone[_buildingIndex]
INT clayPrice = _buildingPricesClay[_buildingIndex]
INT strawPrice = _buildingPricesStraw[_buildingIndex]
INT ironPrice = _buildingPricesIron[_buildingIndex]

LST color = {0.0,0.5,0.0}
IF _totalPlanks LESS planksPrice
    EDIT color = {0.5,0.0,0.0}
IF _totalStone LESS stonePrice
    EDIT color = {0.5,0.0,0.0}
IF _totalClay LESS clayPrice
    EDIT color = {0.5,0.0,0.0}
IF _totalStraw LESS strawPrice
    EDIT color = {0.5,0.0,0.0}
IF _totalIron LESS ironPrice
    EDIT color = {0.5,0.0,0.0}

fe3d:text2d_set_color("price_planks", color[0], color[1], color[2])
fe3d:text2d_set_color("price_stone", color[0], color[1], color[2])
fe3d:text2d_set_color("price_clay", color[0], color[1], color[2])
fe3d:text2d_set_color("price_straw", color[0], color[1], color[2])
fe3d:text2d_set_color("price_iron", color[0], color[1], color[2])

DEC x = fe3d:raycast_get_point_on_terrain_x()
DEC y = fe3d:raycast_get_point_on_terrain_y()
DEC z = fe3d:raycast_get_point_on_terrain_z()

fe3d:model_set_base_position(buildingName, x, y, z)

LST aabbIds = fe3d:model_get_aabb_ids(buildingName)
INT aabbCount = misc:list_size("aabbIds")
INT aabbIndex = 0
DEC aabbPosX = 0.0
DEC aabbPosZ = 0.0
DEC aabbSizeX = 0.0
DEC aabbSizeZ = 0.0
DEC aabbBL = 0.0
DEC aabbBR = 0.0
DEC aabbTR = 0.0
DEC aabbTL = 0.0

LOOP
    IF aabbIndex IS aabbCount
        BREAK
    EDIT aabbPosX = fe3d:model_get_aabb_position_x(buildingName, aabbIds[aabbIndex])
    EDIT aabbPosZ = fe3d:model_get_aabb_position_z(buildingName, aabbIds[aabbIndex])
    EDIT aabbSizeX = fe3d:model_get_aabb_size_x(buildingName, aabbIds[aabbIndex])
    EDIT aabbSizeZ = fe3d:model_get_aabb_size_z(buildingName, aabbIds[aabbIndex])
    DIV aabbSizeX 2.0
    DIV aabbSizeZ 2.0
    SUB aabbPosX aabbSizeX
    SUB aabbPosZ aabbSizeZ
    EDIT aabbBL = fe3d:terrain_get_pixel_height("island", aabbPosX, aabbPosZ)
    ADD aabbPosX aabbSizeX
    ADD aabbPosX aabbSizeX
    EDIT aabbBR = fe3d:terrain_get_pixel_height("island", aabbPosX, aabbPosZ)
    ADD aabbPosZ aabbSizeZ
    ADD aabbPosZ aabbSizeZ
    EDIT aabbTR = fe3d:terrain_get_pixel_height("island", aabbPosX, aabbPosZ)
    SUB aabbPosX aabbSizeX
    SUB aabbPosX aabbSizeX
    EDIT aabbTL = fe3d:terrain_get_pixel_height("island", aabbPosX, aabbPosZ)
    IF aabbBL LESS _buildHeight OR aabbBR LESS _buildHeight OR aabbTR LESS _buildHeight OR aabbTL LESS _buildHeight
        EDIT color = {0.5,0.0,0.0}
    ADD aabbIndex 1

fe3d:collision_calculate_model_aabbs(buildingName)
LST collisions = fe3d:collision_check_model_models(buildingName, "")
INT collisionCount = misc:list_size("collisions")
IF collisionCount MORE 0
    EDIT color = {0.5,0.0,0.0}

LST partIds = fe3d:model_get_part_ids(buildingName)
INT partCount = misc:list_size("partIds")
INT partIndex = 0

LOOP
    IF partIndex IS partCount
        BREAK
    fe3d:model_set_color(buildingName, partIds[partIndex], color[0], color[1], color[2])
    ADD partIndex 1

CAST _buildingCounter STR
BOL clicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
STR newBuildingId = misc:string_concat(buildingName, _buildingCounter)
CAST _buildingCounter INT

IF clicked IS <true> AND color[1] NOT 0.0
    ADD _buildingCounter 1
    fe3d:model_place(newBuildingId, buildingName, 0.0, 0.0, 0.0)
    fe3d:model_set_base_position(newBuildingId, x, y, z)
    SUB _totalPlanks planksPrice
    SUB _totalStone stonePrice
    SUB _totalClay clayPrice
    SUB _totalStraw strawPrice
    SUB _totalIron ironPrice

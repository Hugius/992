101 0
META script_type_update
META script_state_wait

IF _buildMode IS <false> OR _previousBuildMode IS <false>
    EXIT

fe3d:raycast_calculate_terrain_intersection(100.0, 0.1)

STR buildingModel = _buildingModels[_buildingIndex]
STR buildingType = _buildingTypes[_buildingIndex]
INT planksPrice = _buildPricesPlanks[_buildingIndex]
INT stonePrice = _buildPricesStone[_buildingIndex]
INT clayPrice = _buildPricesClay[_buildingIndex]
INT strawPrice = _buildPricesStraw[_buildingIndex]

LST aabbIds = fe3d:model_get_aabb_ids(buildingType)
LST partIds = fe3d:model_get_part_ids(buildingType)
DEC rotation = fe3d:model_get_base_rotation_y(buildingType)
DEC x = fe3d:raycast_get_point_on_terrain_x()
DEC y = fe3d:raycast_get_point_on_terrain_y()
DEC z = fe3d:raycast_get_point_on_terrain_z()
BOL lmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOL rmbClicked = fe3d:input_is_mouse_pressed("BUTTON_RIGHT")

LST color = {0.0,0.5,0.0}
DEC aabbPosX = 0.0
DEC aabbPosZ = 0.0
DEC aabbSizeX = 0.0
DEC aabbSizeZ = 0.0
DEC aabbBL = 0.0
DEC aabbBR = 0.0
DEC aabbTR = 0.0
DEC aabbTL = 0.0
INT index = 0
INT maxIndex = 0

IF rmbClicked IS <true>
    ADD rotation 90.0

fe3d:model_set_base_position(buildingType, x, y, z)
fe3d:model_set_base_rotation(buildingType, 0.0, rotation, 0.0)

IF _totalPlanks LESS planksPrice
    EDIT color = {0.5,0.0,0.0}
IF _totalStone LESS stonePrice
    EDIT color = {0.5,0.0,0.0}
IF _totalClay LESS clayPrice
    EDIT color = {0.5,0.0,0.0}
IF _totalStraw LESS strawPrice
    EDIT color = {0.5,0.0,0.0}

fe3d:text2d_set_color("price_planks", color[0], color[1], color[2])
fe3d:text2d_set_color("price_stone", color[0], color[1], color[2])
fe3d:text2d_set_color("price_clay", color[0], color[1], color[2])
fe3d:text2d_set_color("price_straw", color[0], color[1], color[2])

fe3d:collision_calculate_model_aabbs(buildingType)
LST collisions = fe3d:collision_check_model_models(buildingType, "")
INT collisionCount = misc:list_size("collisions")
IF collisionCount MORE 0
    EDIT color = {0.5,0.0,0.0}

EDIT index = 0
EDIT maxIndex = misc:list_size("aabbIds")
LOOP
    IF index IS maxIndex
        BREAK
    EDIT aabbPosX = fe3d:model_get_aabb_position_x(buildingType, aabbIds[index])
    EDIT aabbPosZ = fe3d:model_get_aabb_position_z(buildingType, aabbIds[index])
    EDIT aabbSizeX = fe3d:model_get_aabb_size_x(buildingType, aabbIds[index])
    EDIT aabbSizeZ = fe3d:model_get_aabb_size_z(buildingType, aabbIds[index])
    DIV aabbSizeX 2.0
    DIV aabbSizeZ 2.0
    SUB aabbPosX aabbSizeX
    SUB aabbPosZ aabbSizeZ
    EDIT aabbBL = fe3d:terrain_get_pixel_height("island", aabbPosX, aabbPosZ)
    ADD aabbPosX aabbSizeX
    ADD aabbPosX aabbSizeX
    EDIT aabbBR = fe3d:terrain_get_pixel_height("island", aabbPosX, aabbPosZ)
    ADD aabbPosZ aabbSizeZ
    ADD aabbPosZ aabbSizeZ
    EDIT aabbTR = fe3d:terrain_get_pixel_height("island", aabbPosX, aabbPosZ)
    SUB aabbPosX aabbSizeX
    SUB aabbPosX aabbSizeX
    EDIT aabbTL = fe3d:terrain_get_pixel_height("island", aabbPosX, aabbPosZ)
    IF aabbBL LESS _buildHeight OR aabbBR LESS _buildHeight OR aabbTR LESS _buildHeight OR aabbTL LESS _buildHeight
        EDIT color = {0.5,0.0,0.0}
    ADD index 1

EDIT index = 0
EDIT maxIndex = misc:list_size("partIds")
LOOP
    IF index IS maxIndex
        BREAK
    fe3d:model_set_color(buildingType, partIds[index], color[0], color[1], color[2])
    ADD index 1

CAST _buildingCounter STR
STR buildingId = misc:string_concat(buildingType, "_")
EDIT buildingId = misc:string_concat(buildingId, _buildingCounter)
CAST _buildingCounter INT

IF lmbClicked IS <true> AND color[1] NOT 0.0
    fe3d:model_place(buildingId, buildingModel, 0.0, 0.0, 0.0)
    fe3d:model_set_base_position(buildingId, x, y, z)
    fe3d:model_set_base_rotation(buildingId, 0.0, rotation, 0.0)
    SUB _totalPlanks planksPrice
    SUB _totalStone stonePrice
    SUB _totalClay clayPrice
    SUB _totalStraw strawPrice
    ADD _buildingCounter 1
    PUSH _buildingIds buildingId
    IF buildingType NOT "grave"
        IF buildingType NOT "house"
            IF buildingType NOT "storage"
                IF buildingType NOT "tavern"
                    INT workerIndex = misc:list_index("_workerBuildingIds", "")
                    IF workerIndex NOT -1
                        EDIT _workerBuildingIds[workerIndex] = buildingId
    IF buildingType IS "apiarist"
        PUSH _apiaristIds buildingId
        PUSH _apiaristTimers 0
    ELIF buildingType IS "baker"
        PUSH _bakerIds buildingId
    ELIF buildingType IS "barber"
        PUSH _barberIds buildingId
    ELIF buildingType IS "blacksmith"
        PUSH _blacksmithIds buildingId
    ELIF buildingType IS "carpenter"
        PUSH _carpenterIds buildingId
    ELIF buildingType IS "mason"
        PUSH _masonIds buildingId
    ELIF buildingType IS "roofer"
        PUSH _rooferIds buildingId
    ELIF buildingType IS "storage"
        PUSH _storageIds buildingId
        ADD _maxWater 10
        ADD _maxAle 10
        ADD _maxMilk 10
        ADD _maxSmokedMeat 10
        ADD _maxSmokedFish 10
        ADD _maxBread 10
        ADD _maxVegetables 10
        ADD _maxEggs 10
        ADD _maxRawFish 10
        ADD _maxRawMeat 10
        ADD _maxHoney 10
        ADD _maxFruit 10
        ADD _maxLumber 10
        ADD _maxStone 10
        ADD _maxGrain 10
        ADD _maxFlour 10
        ADD _maxPlanks 10
        ADD _maxFirewood 10
        ADD _maxManure 10
        ADD _maxOre 10
        ADD _maxIron 10
        ADD _maxHides 10
        ADD _maxClothes 10
        ADD _maxWool 10
        ADD _maxLeather 10
        ADD _maxClay 10
        ADD _maxStraw 10
        ADD _maxMedicine 10
        ADD _maxFeathers 10
        ADD _maxMushrooms 10
        ADD _maxGrainSeeds 10
        ADD _maxVegetableSeeds 10
        ADD _maxTools 5
        ADD _maxBows 5
        ADD _maxArrows 50

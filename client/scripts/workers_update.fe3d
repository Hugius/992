78 19
META script_type_update
META script_state_wait

/// Values
INT index = 0
INT maxIndex = 0
INT secondsLeft = _totalSeconds
MOD secondsLeft 86400
BOL newDay = (_totalSeconds NOT 0 AND secondsLeft IS 0)

/// Creating new workers
IF _workersToCreate MORE 0
    SUB _workersToCreate 1
    STR newWorkerName = _availableWorkerNames[0]
    PULL _availableWorkerNames 0
    PUSH _workerNames newWorkerName
    PUSH _workerHealths <true>
    EDIT index = 0
    EDIT maxIndex = misc:list_size("_buildingIds")
    LOOP
        IF index IS maxIndex
            PUSH _workerBuildingIds ""
            BREAK
        EDIT hasWorker = misc:list_contains("_workerBuildingIds", _buildingIds[index])
        IF hasWorker IS <false>
            PUSH _workerBuildingIds _buildingIds[index]
            BREAK
        ADD index 1

/// Death by suicide
IF newDay IS <true>
    PASS

/// Death by sickness
IF newDay IS <true>
    EDIT index = 0
    EDIT maxIndex = misc:list_size("_workerNames")
    LOOP
        IF index IS maxIndex
            BREAK
        IF _workerHealths[index] IS <false>
            IF _totalMedicine MORE 0
                SUB _totalMedicine 1
                EDIT _workerHealths[index] = <true>
            ELSE
                PUSH _availableWorkerNames _workerNames[index]
                PULL _workerNames index
                PULL _workerHealths index
                PULL _workerBuildingIds index
                ADD _totalDeadWorkers 1
                SUB index 1
                SUB maxIndex 1
        ADD index 1

/// Death by dehydration
IF newDay IS <true>
    EDIT index = 0
    EDIT maxIndex = misc:list_size("_workerNames")
    LOOP
        IF index IS maxIndex
            BREAK
        IF _totalWater MORE 0
            SUB _totalWater 1
        ELIF _totalAle MORE 0
            SUB _totalAle 1
        ELIF _totalMilk MORE 0
            SUB _totalMilk 1
        ELSE
            PUSH _availableWorkerNames _workerNames[index]
            PULL _workerNames index
            PULL _workerHealths index
            PULL _workerBuildingIds index
            ADD _totalDeadWorkers 1
            SUB index 1
            SUB maxIndex 1
        ADD index 1

/// Death by starvation
IF newDay IS <true>
    EDIT index = 0
    EDIT maxIndex = misc:list_size("_workerNames")
    LOOP
        IF index IS maxIndex
            BREAK
        IF _totalSmokedMeat MORE 0
            SUB _totalSmokedMeat 1
        ELIF _totalSmokedFish MORE 0
            SUB _totalSmokedFish 1
        ELIF _totalBread MORE 0
            SUB _totalBread 1
        ELIF _totalVegetables MORE 0
            SUB _totalVegetables 1
        ELIF _totalEggs MORE 0
            SUB _totalEggs 1
        ELIF _totalRawMeat MORE 0
            SUB _totalRawMeat 1
        ELIF _totalRawFish MORE 0
            SUB _totalRawFish 1
        ELIF _totalHoney MORE 0
            SUB _totalHoney 1
        ELIF _totalFruit MORE 0
            SUB _totalFruit 1
        ELSE
            PUSH _availableWorkerNames _workerNames[index]
            PULL _workerNames index
            PULL _workerHealths index
            PULL _workerBuildingIds index
            ADD _totalDeadWorkers 1
            SUB index 1
            SUB maxIndex 1
        ADD index 1

/// Spreading sickness
IF newDay IS <true>
    INT randomNumber = 0
    INT sicknessChance = 0
    EDIT index = 0
    EDIT maxIndex = misc:list_size("_workerNames")
    LOOP
        IF index IS maxIndex
            BREAK
        EDIT randomNumber = misc:random_integer(1, 100)
        EDIT sicknessChance = 100
        SUB sicknessChance _sicknessChance
        IF randomNumber MORE sicknessChance
            EDIT _workerHealths[index] = <false>
        ADD index 1

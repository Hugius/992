3 22
META script_type_update
META script_state_waiting

/// Set default chance
ALT _sicknessCatchChance = 15

/// Validate
IF _mustUpdateWorkers IS <false>
  EXIT
ELSE
  ALT _mustUpdateWorkers = <false>

/// Define reusable values
DEF STR eventMessage = ""
DEF STR buildingId = ""
DEF STR buildingType = ""
DEF INT workerIndex = 0
DEF INT buildingIndex = 0
DEF INT maxWorkerIndex = misc:list_size("_workerNames")
DEF INT maxBuildingIndex = misc:list_size("_placedBuildingIds")

/// Assign workers to job building
DEF BOL buildingInUse = <false>
ALT workerIndex = 0
LOOP
  IF workerIndex IS maxWorkerIndex
    BREAK
  IF _workerJobBuildingIds[workerIndex] NOT ""
    ADD workerIndex 1
    CONTINUE
  ALT buildingIndex = 0
  LOOP
    IF buildingIndex IS maxBuildingIndex
      BREAK
    ALT buildingId = _placedBuildingIds[buildingIndex]
    ALT buildingType = _placedBuildingTypes[buildingIndex]
    IF buildingType IS "grave"
      ADD buildingIndex 1
      CONTINUE
    IF buildingType IS "house"
      ADD buildingIndex 1
      CONTINUE
    IF buildingType IS "storage"
      ADD buildingIndex 1
      CONTINUE
    IF buildingType IS "tavern"
      ADD buildingIndex 1
      CONTINUE
    ALT buildingInUse = misc:list_contains("_workerJobBuildingIds", buildingId)
    IF buildingInUse IS <false>
      ALT _workerJobBuildingIds[workerIndex] = buildingId
      ALT eventMessage = misc:string_concat(_workerNames[workerIndex], " became ")
      ALT eventMessage = misc:string_concat(eventMessage, buildingType)
      PUSH _eventMessageContents eventMessage
      BREAK
    ADD buildingIndex 1
  ADD workerIndex 1

/// Assign workers to house building
DEF STR workerName = ""
DEF STR workerGender = ""
DEF STR otherWorkerName = ""
DEF STR otherWorkerGender = ""
DEF INT otherWorkerIndex = 0
DEF INT workerCount = 0
ALT workerIndex = 0
LOOP
  IF workerIndex IS maxWorkerIndex
    BREAK
  IF _workerHouseBuildingIds[workerIndex] NOT ""
    ADD workerIndex 1
    CONTINUE
  ALT workerName = _workerNames[workerIndex]
  ALT workerGender = _workerGenders[workerIndex]
  ALT buildingIndex = 0
  LOOP
    IF buildingIndex IS maxBuildingIndex
      BREAK
    ALT buildingId = _placedBuildingIds[buildingIndex]
    ALT buildingType = _placedBuildingTypes[buildingIndex]
    IF buildingType NOT "house"
      ADD buildingIndex 1
      CONTINUE
    ALT workerCount = misc:list_count("_workerHouseBuildingIds", buildingId)
    IF workerCount IS 0
      ALT _workerHouseBuildingIds[workerIndex] = buildingId
      BREAK
    ELIF workerCount IS 1
      ALT _workerHouseBuildingIds[workerIndex] = buildingId
      ALT otherWorkerIndex = misc:list_index("_workerHouseBuildingIds", buildingId)
      ALT otherWorkerName = _workerNames[otherWorkerIndex]
      ALT otherWorkerGender = _workerGenders[otherWorkerIndex]
      IF workerGender IS "MALE" AND otherWorkerGender IS "FEMALE"
        ALT eventMessage = misc:string_concat(workerName, " married ")
        ALT eventMessage = misc:string_concat(eventMessage, otherWorkerName)
        PUSH _eventMessageContents eventMessage
        BREAK
      IF workerGender IS "FEMALE" AND otherWorkerGender IS "MALE"
        ALT eventMessage = misc:string_concat(otherWorkerName, " married ")
        ALT eventMessage = misc:string_concat(eventMessage, workerName)
        PUSH _eventMessageContents eventMessage
        BREAK
    ELIF workerCount IS 2
      PASS
    ELSE
      CRASH
    ADD buildingIndex 1
  ADD workerIndex 1

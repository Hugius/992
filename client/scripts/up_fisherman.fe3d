26 58
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasFishingNet = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "fisherman" OR _firstUpdate IS <true>
  fe3d:quad2d_set_visible("fisherman_worker", <false>)
  fe3d:quad2d_set_visible("fisherman_fishingnets", <false>)
  fe3d:quad2d_set_visible("fisherman_time", <false>)
  fe3d:quad2d_set_visible("fisherman_convert", <false>)
  fe3d:quad2d_set_visible("fisherman_rawfish", <false>)
  fe3d:text2d_set_visible("fisherman_worker", <false>)
  fe3d:text2d_set_visible("fisherman_fishingnets", <false>)
  fe3d:text2d_set_visible("fisherman_rawfish", <false>)

/// Show GUI
IF _selectedBuildingType IS "fisherman"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  ALT hasFishingNet = misc:list_contains("_fishingNetBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    IF hasFishingNet IS <true>
      fe3d:quad2d_set_visible("fisherman_time", <true>)
      fe3d:quad2d_set_visible("fisherman_convert", <true>)
      fe3d:quad2d_set_visible("fisherman_rawfish", <true>)
      fe3d:text2d_set_visible("fisherman_rawfish", <true>)
    ELSE
      fe3d:quad2d_set_visible("fisherman_fishingnets", <true>)
      fe3d:text2d_set_visible("fisherman_fishingnets", <true>)
      IF _totalFishingNets MORE 0
        ALT _cursorType = "HAND"
        DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
        IF isLmbClicked IS <true>
          SUB _totalFishingNets 1
          PUSH _fishingNetBuildingIds _selectedBuildingId
  ELSE
    fe3d:quad2d_set_visible("fisherman_worker", <true>)
    fe3d:text2d_set_visible("fisherman_worker", <true>)

/// Update fishermen
DEF INT maxFishermanIndex = misc:list_size("_fishermanIds")
DEF INT fishermanIndex = 0
LOOP
  IF fishermanIndex IS maxFishermanIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _fishermanIds[fishermanIndex])
  ALT hasFishingNet = misc:list_contains("_fishingNetBuildingIds", _fishermanIds[fishermanIndex])
  IF hasWorker IS <true> AND hasFishingNet IS <true>
    IF _fishermanTimers[fishermanIndex] IS _productionTime
      ALT _fishermanTimers[fishermanIndex] = 0
      ADD _totalRawFish 1
    ELSE
      ADD _fishermanTimers[fishermanIndex] 1
  ADD fishermanIndex 1

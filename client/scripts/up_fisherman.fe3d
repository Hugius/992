0 0
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasFishingNet = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "fisherman" OR _firstUpdate IS <true>
    fe3d:quad2d_set_visible("fisherman_worker", <false>)
    fe3d:quad2d_set_visible("fisherman_fishingnet", <false>)
    fe3d:quad2d_set_visible("fisherman_time", <false>)
    fe3d:quad2d_set_visible("fisherman_convert", <false>)
    fe3d:quad2d_set_visible("fisherman_rawfish", <false>)
    fe3d:text2d_set_visible("fisherman_time", <false>)
    fe3d:text2d_set_visible("fisherman_worker", <false>)
    fe3d:text2d_set_visible("fisherman_fishingnet", <false>)
    fe3d:text2d_set_visible("fisherman_rawfish", <false>)

/// Show GUI
IF _selectedBuildingType IS "fisherman"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    ALT hasFishingNet = misc:list_contains("_fishingNetBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        IF hasFishingNet IS <true>
            fe3d:quad2d_set_visible("fisherman_time", <true>)
            fe3d:quad2d_set_visible("fisherman_convert", <true>)
            fe3d:quad2d_set_visible("fisherman_rawfish", <true>)
            fe3d:text2d_set_visible("fisherman_time", <true>)
            fe3d:text2d_set_visible("fisherman_rawfish", <true>)
        ELSE
            fe3d:quad2d_set_visible("miner_fishingnet", <true>)
            fe3d:text2d_set_visible("miner_fishingnet", <true>)
            IF _totalFishingNets MORE 0
                ALT _cursorType = "HAND"
                DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
                IF isLmbClicked IS <true>
                    SUB _totalFishingNets 1
                    PUSH _fishingNetBuildingIds _selectedBuildingId
    ELSE
        fe3d:quad2d_set_visible("fisherman_worker", <true>)
        fe3d:text2d_set_visible("fisherman_worker", <true>)

/// Update fishermen
DEF INT maxIndex = misc:list_size("_fishermanIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _fishermanIds[index])
    ALT hasFishingNet = misc:list_contains("_fishingNetBuildingIds", _fishermanIds[index])
    IF hasWorker IS <true> AND hasFishingNet IS <true>
        IF _fishermanTimers[index] IS _fishermanInterval
            ALT _fishermanTimers[index] = 0
            IF _totalRawFish LESS _maxRawFish
                ADD _totalRawFish 1
        ELSE
            ADD _fishermanTimers[index] 1
    ADD index 1

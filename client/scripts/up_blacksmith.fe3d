47 44
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "blacksmith" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("blacksmith_worker", <false>)
    fe3d:quad2d_set_visible("blacksmith_planks", <false>)
    fe3d:quad2d_set_visible("blacksmith_iron", <false>)
    fe3d:quad2d_set_visible("blacksmith_convert", <false>)
    fe3d:quad2d_set_visible("blacksmith_tools", <false>)
    fe3d:text2d_set_visible("blacksmith_worker", <false>)
    fe3d:text2d_set_visible("blacksmith_planks", <false>)
    fe3d:text2d_set_visible("blacksmith_iron", <false>)
    fe3d:text2d_set_visible("blacksmith_tools", <false>)

/// Show GUI
IF _selectedBuildingType IS "blacksmith"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("blacksmith_planks", <true>)
        fe3d:quad2d_set_visible("blacksmith_iron", <true>)
        fe3d:quad2d_set_visible("blacksmith_convert", <true>)
        fe3d:quad2d_set_visible("blacksmith_tools", <true>)
        fe3d:text2d_set_visible("blacksmith_planks", <true>)
        fe3d:text2d_set_visible("blacksmith_iron", <true>)
        fe3d:text2d_set_visible("blacksmith_tools", <true>)
    ELSE
        fe3d:quad2d_set_visible("blacksmith_worker", <true>)
        fe3d:text2d_set_visible("blacksmith_worker", <true>)

/// Update blacksmiths
DEF INT maxIndex = misc:list_size("_blacksmithIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _blacksmithIds[index])
    IF hasWorker IS <true>
        IF _blacksmithTimers[index] IS 0
            IF _totalPlanks MORE 0 AND _totalIron MORE 0
                SUB _totalPlanks 1
                SUB _totalIron 1
                ADD _blacksmithTimers[index] 1
        ELIF _blacksmithTimers[index] IS _blacksmithInterval
            ALT _blacksmithTimers[index] = 0
            IF _totalTools LESS _maxTools
                ADD _totalTools 1
        ELSE
            ADD _blacksmithTimers[index] 1
    ADD index 1

0 0
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>
DEF BOL hasTool = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "bowyer" OR _firstUpdate IS <true>
  fe3d:quad2d_set_visible("bowyer_worker", <false>)
  fe3d:quad2d_set_visible("bowyer_tool", <false>)
  fe3d:quad2d_set_visible("bowyer_planks", <false>)
  fe3d:quad2d_set_visible("bowyer_wool", <false>)
  fe3d:quad2d_set_visible("bowyer_convert", <false>)
  fe3d:quad2d_set_visible("bowyer_bows", <false>)
  fe3d:text2d_set_visible("bowyer_worker", <false>)
  fe3d:text2d_set_visible("bowyer_tool", <false>)
  fe3d:text2d_set_visible("bowyer_planks", <false>)
  fe3d:text2d_set_visible("bowyer_wool", <false>)
  fe3d:text2d_set_visible("bowyer_bows", <false>)

/// Show GUI
IF _selectedBuildingType IS "bowyer"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  ALT hasTool = misc:list_contains("_toolBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    IF hasTool IS <true>
      fe3d:quad2d_set_visible("bowyer_planks", <true>)
      fe3d:quad2d_set_visible("bowyer_wool", <true>)
      fe3d:quad2d_set_visible("bowyer_convert", <true>)
      fe3d:quad2d_set_visible("bowyer_bows", <true>)
      fe3d:text2d_set_visible("bowyer_planks", <true>)
      fe3d:text2d_set_visible("bowyer_wool", <true>)
      fe3d:text2d_set_visible("bowyer_bows", <true>)
    ELSE
      fe3d:quad2d_set_visible("bowyer_tool", <true>)
      fe3d:text2d_set_visible("bowyer_tool", <true>)
      IF _totalTools MORE 0
        ALT _cursorType = "HAND"
        DEF BOL isLmbClicked = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
        IF isLmbClicked IS <true>
          SUB _totalTools 1
          PUSH _toolBuildingIds _selectedBuildingId
  ELSE
    fe3d:quad2d_set_visible("bowyer_worker", <true>)
    fe3d:text2d_set_visible("bowyer_worker", <true>)

/// Update bowyers
DEF INT maxBowyerIndex = misc:list_size("_bowyerIds")
DEF INT bowyerIndex = 0
LOOP
  IF bowyerIndex IS maxBowyerIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _bowyerIds[bowyerIndex])
  ALT hasTool = misc:list_contains("_toolBuildingIds", _bowyerIds[bowyerIndex])
  IF hasWorker IS <true> AND hasTool IS <true>
    IF _bowyerTimers[bowyerIndex] IS 0
      IF _totalPlanks MORE 0 AND _totalWool MORE 0
        SUB _totalPlanks 1
        SUB _totalWool 1
        ADD _bowyerTimers[bowyerIndex] 1
    ELIF _bowyerTimers[bowyerIndex] IS _productionTime
      ALT _bowyerTimers[bowyerIndex] = 0
      ADD _totalBows 1
    ELSE
      ADD _bowyerTimers[bowyerIndex] 1
  ADD bowyerIndex 1

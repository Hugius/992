48 40
META script_type_update
META script_state_wait

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "baker" OR _totalUpdates IS 0
    fe3d:quad2d_set_visible("baker_worker", <false>)
    fe3d:quad2d_set_visible("baker_flour", <false>)
    fe3d:quad2d_set_visible("baker_firewood", <false>)
    fe3d:quad2d_set_visible("baker_convert", <false>)
    fe3d:quad2d_set_visible("baker_bread", <false>)
    fe3d:text2d_set_visible("baker_worker", <false>)
    fe3d:text2d_set_visible("baker_flour", <false>)
    fe3d:text2d_set_visible("baker_firewood", <false>)
    fe3d:text2d_set_visible("baker_bread", <false>)

/// Show GUI
IF _selectedBuildingType IS "baker"
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
    IF hasWorker IS <true>
        fe3d:quad2d_set_visible("baker_flour", <true>)
        fe3d:quad2d_set_visible("baker_firewood", <true>)
        fe3d:quad2d_set_visible("baker_convert", <true>)
        fe3d:quad2d_set_visible("baker_bread", <true>)
        fe3d:text2d_set_visible("baker_flour", <true>)
        fe3d:text2d_set_visible("baker_firewood", <true>)
        fe3d:text2d_set_visible("baker_bread", <true>)
    ELSE
        fe3d:quad2d_set_visible("baker_worker", <true>)
        fe3d:text2d_set_visible("baker_worker", <true>)

/// Update bakers
DEF INT maxIndex = misc:list_size("_bakerIds")
DEF INT index = 0
LOOP
    IF index IS maxIndex
        BREAK
    ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _bakerIds[index])
    IF hasWorker IS <true>
        IF _bakerTimers[index] IS _bakerInterval
            IF _totalFlour MORE 0 AND _totalFirewood MORE 0
                ADD _totalBread 10
                IF _totalBread MORE _maxBread
                    SUB _totalBread 10
                ELSE
                    SUB _totalFlour 1
                    SUB _totalFirewood 1
                    ALT _bakerTimers[index] = 0
        ELSE
            ADD _bakerTimers[index] 1
    ADD index 1

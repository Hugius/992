74 11
META script_type_update
META script_state_wait

/// Update creation count
IF _workersToCreate IS 0
    EDIT _creatingDefaultWorkers = <false>
    EXIT
ELSE
    SUB _workersToCreate 1

/// Define reusable values
STR eventMessage = ""
INT index = 0
INT maxIndex = 0

/// Determine name and gender
IF _nextWorkerGender IS 1
    INT workerNameCount = misc:list_size("_maleWorkerNames")
    INT workerNameIndex = math:random_integer(1, workerNameCount)
    SUB workerNameIndex 1
    STR workerName = _maleWorkerNames[workerNameIndex]
    STR workerGender = "MALE"
ELSE
    INT workerNameCount = misc:list_size("_femaleWorkerNames")
    INT workerNameIndex = math:random_integer(1, workerNameCount)
    SUB workerNameIndex 1
    STR workerName = _femaleWorkerNames[workerNameIndex]
    STR workerGender = "FEMALE"

/// Switch gender
NEG _nextWorkerGender

/// Create new worker
PUSH _workerNames workerName
PUSH _workerGenders workerGender
PUSH _workerHealths <true>

/// Log birth event
IF _creatingDefaultWorkers IS <false>
    EDIT eventMessage = misc:string_concat(workerName, " was born")
    EDIT _eventMessageContent = eventMessage
    EDIT _eventMessageOpacity = 1.0

/// Assign worker to job building
BOL hasWorker = <false>
EDIT index = 0
EDIT maxIndex = misc:list_size("_placedBuildingIds")
LOOP
    IF index IS maxIndex
        PUSH _workerJobBuildingIds ""
        BREAK
    IF _placedBuildingTypes[index] IS "grave"
        ADD index 1
        CONTINUE
    IF _placedBuildingTypes[index] IS "house"
        ADD index 1
        CONTINUE
    IF _placedBuildingTypes[index] IS "storage"
        ADD index 1
        CONTINUE
    IF _placedBuildingTypes[index] IS "tavern"
        ADD index 1
        CONTINUE
    EDIT hasWorker = misc:list_contains("_workerJobBuildingIds", _placedBuildingIds[index])
    IF hasWorker IS <false>
        PUSH _workerJobBuildingIds _placedBuildingIds[index]
        IF _creatingDefaultWorkers IS <false>
            STR eventMessage = misc:string_concat(_workerNames[index], " became ")
            EDIT eventMessage = misc:string_concat(eventMessage, _placedBuildingTypes[index])
            EDIT _eventMessageContent = eventMessage
            EDIT _eventMessageOpacity = 1.0
        BREAK
    ADD index 1

/// Assign worker to house building
INT workerCount = 0
INT workerIndex = 0
EDIT index = 0
EDIT maxIndex = misc:list_size("_placedBuildingIds")
LOOP
    IF index IS maxIndex
        PUSH _workerHouseBuildingIds ""
        BREAK
    IF _placedBuildingTypes[index] NOT "house"
        ADD index 1
        CONTINUE
    EDIT workerCount = misc:list_count("_workerHouseBuildingIds", _placedBuildingIds[index])
    IF workerCount IS 0
        PUSH _workerHouseBuildingIds _placedBuildingIds[index]
        BREAK
    ELIF workerCount IS 1
        PUSH _workerHouseBuildingIds _placedBuildingIds[index]
        EDIT workerIndex = misc:list_index("_workerHouseBuildingIds", _placedBuildingIds[index])
        IF _workerGenders[workerIndex] IS "MALE" AND workerGender IS "FEMALE"
            EDIT eventMessage = misc:string_concat(_workerNames[workerIndex], " married ")
            EDIT eventMessage = misc:string_concat(eventMessage, workerName)
            EDIT _eventMessageContent = eventMessage
            EDIT _eventMessageOpacity = 1.0
            BREAK
        IF _workerGenders[workerIndex] IS "FEMALE" AND workerGender IS "MALE"
            EDIT eventMessage = misc:string_concat(workerName, " married ")
            EDIT eventMessage = misc:string_concat(eventMessage, _workerNames[workerIndex])
            EDIT _eventMessageContent = eventMessage
            EDIT _eventMessageOpacity = 1.0
            BREAK
    ELIF workerCount IS 2
        PASS
    ELSE
        CRASH
    ADD index 1

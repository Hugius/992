36 0
META script_type_update
META script_state_wait

/// Update creation count
IF _workersToCreate IS 0
    ALT _creatingDefaultWorkers = <false>
    EXIT
ELSE
    SUB _workersToCreate 1

/// Determine name and gender
IF _nextWorkerGender IS 1
    DEF INT nameCount = misc:list_size("_maleWorkerNames")
    DEF INT nameIndex = math:random_integer(1, nameCount)
    SUB nameIndex 1
    DEF STR name = _maleWorkerNames[nameIndex]
    DEF STR gender = "MALE"
ELSE
    DEF INT nameCount = misc:list_size("_femaleWorkerNames")
    DEF INT nameIndex = math:random_integer(1, nameCount)
    SUB nameIndex 1
    DEF STR name = _femaleWorkerNames[nameIndex]
    DEF STR gender = "FEMALE"

/// Switch gender
NEG _nextWorkerGender

/// Determine clothes
IF _creatingDefaultWorkers IS <true>
    DEF BOL clothes = <true>
ELSE
    IF _totalClothes MORE 0
        SUB _totalClothes 1
        DEF BOL clothes = <true>
    ELSE
        DEF BOL clothes = <false>

/// Create worker
PUSH _workerNames name
PUSH _workerGenders gender
PUSH _workerHealths <true>
PUSH _workerClothes clothes
PUSH _workerJobBuildingIds ""
PUSH _workerHouseBuildingIds ""

/// Update workers
ALT _updateWorkers = <true>

/// Log birth event
IF _creatingDefaultWorkers IS <false>
    DEF STR eventMessage = misc:string_concat(name, " was born")
    PUSH _eventMessageContents eventMessage

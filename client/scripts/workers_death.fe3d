64 4
META script_type_update
META script_state_wait

/// Update death timer
IF _workerDeathTimer IS _workerDeathTimespan
    ALT _workerDeathTimer = 0
ELSE
    ADD _workerDeathTimer 1
    EXIT

/// Define reusable values
DEF FIN STR workerName = _workerNames[_workerDeathIndex]
DEF FIN INT randomPercentage = math:random_integer(1, 100)
DEF FIN INT priestCount = misc:list_size("_priestIds")
DEF FIN INT tavernCount = misc:list_size("_tavernIds")
DEF STR eventMessage = ""
DEF BOL workerIsDead = <false>

/// Update dying from childbirth
IF workerIsDead IS <false> AND priestCount IS 0
    DEF INT houseIndex = misc:list_index("_houseIdsThatGaveBirth", _workerHouseBuildingIds[_workerDeathIndex])
    IF houseIndex NOT -1
        PULL _houseIdsThatGaveBirth houseIndex
        DEF INT birthDeathChance = 100
        SUB birthDeathChance _birthDeathChance
        IF randomPercentage MORE birthDeathChance
            ALT workerIsDead = <true>
            ALT eventMessage = misc:string_concat(workerName, " died of childbirth")
            ALT _eventMessageContent = eventMessage
            ALT _eventMessageOpacity = 1.0

/// Update dying from suicide
IF workerIsDead IS <false> AND priestCount IS 0 AND tavernCount IS 0
    DEF INT suicideChance = 100
    SUB suicideChance _suicideDeathChance
    IF randomPercentage MORE suicideChance
        ALT workerIsDead = <true>
        ALT eventMessage = misc:string_concat(workerName, " died of suicide")
        ALT _eventMessageContent = eventMessage
        ALT _eventMessageOpacity = 1.0

/// Update dying from sickness
IF workerIsDead IS <false>
    IF _workerHealths[_workerDeathIndex] IS <true>
        IF priestCount IS 0
            DEF INT sicknessChance = 100
            SUB sicknessChance _sicknessCatchChance
            IF randomPercentage MORE sicknessChance
                ALT _workerHealths[_workerDeathIndex] = <false>
                ALT eventMessage = misc:string_concat(workerName, " caught the sickness")
                ALT _eventMessageContent = eventMessage
                ALT _eventMessageOpacity = 1.0
    ELSE
        IF _totalMedicine MORE 0
            SUB _totalMedicine 1
            ALT _workerHealths[_workerDeathIndex] = <true>
        ELSE
            ALT workerIsDead = <true>
            ALT eventMessage = misc:string_concat(workerName, " died of sickness")
            ALT _eventMessageContent = eventMessage
            ALT _eventMessageOpacity = 1.0

/// Update dying from dehydration
IF workerIsDead IS <false>
    IF _totalAle MORE 0
        SUB _totalAle 1
    ELIF _totalMilk MORE 0
        SUB _totalMilk 1
    ELSE
        ALT workerIsDead = <true>
        ALT eventMessage = misc:string_concat(workerName, " died of dehydration")
        ALT _eventMessageContent = eventMessage
        ALT _eventMessageOpacity = 1.0

/// Update dying from starvation
IF workerIsDead IS <false>
    IF _totalSmokedMeat MORE 0
        SUB _totalSmokedMeat 1
    ELIF _totalSmokedFish MORE 0
        SUB _totalSmokedFish 1
    ELIF _totalBread MORE 0
        SUB _totalBread 1
    ELIF _totalVegetables MORE 0
        SUB _totalVegetables 1
    ELIF _totalEggs MORE 0
        SUB _totalEggs 1
    ELIF _totalRawMeat MORE 0
        SUB _totalRawMeat 1
    ELIF _totalRawFish MORE 0
        SUB _totalRawFish 1
    ELIF _totalHoney MORE 0
        SUB _totalHoney 1
    ELIF _totalFruit MORE 0
        SUB _totalFruit 1
    ELSE
        ALT workerIsDead = <true>
        ALT eventMessage = misc:string_concat(workerName, " died of starvation")
        ALT _eventMessageContent = eventMessage
        ALT _eventMessageOpacity = 1.0

/// Update dying from hypothermia
IF workerIsDead IS <false>
    IF _workerClothes[_workerDeathIndex] IS <false>
        ALT workerIsDead = <true>
        ALT eventMessage = misc:string_concat(workerName, " died of hypothermia")
        ALT _eventMessageContent = eventMessage
        ALT _eventMessageOpacity = 1.0

/// Handle dead worker
IF workerIsDead IS <true>
    PULL _workerNames _workerDeathIndex
    PULL _workerGenders _workerDeathIndex
    PULL _workerClothes _workerDeathIndex
    PULL _workerHealths _workerDeathIndex
    PULL _workerJobBuildingIds _workerDeathIndex
    PULL _workerHouseBuildingIds _workerDeathIndex
    ADD _totalDeadWorkers 1
ELSE
    ADD _workerDeathIndex 1

/// Handle dead population
DEF INT totalWorkers = misc:list_size("_workerNames")
IF totalWorkers IS 0
    fe3d:application_stop()

/// Update event index
IF _workerDeathIndex IS totalWorkers
    ALT _workerDeathIndex = 0
    ALT _workerDeathTimespan = _workersDeathTimespan
    DIV _workerDeathTimespan totalWorkers

/// Set default sickness chance
ALT _sicknessCatchChance = 15

116 20
META script_type_update
META script_state_wait

/// Update event timer
IF _workerDeathTimer IS _workerDeathTimespan
    EDIT _workerDeathTimer = 0
ELSE
    ADD _workerDeathTimer 1
    EXIT

/// Prepare values
STR workerName = _workerNames[_workerDeathIndex]
STR workerGender = _workerGenders[_workerDeathIndex]
STR workerHouseId = _workerHouseBuildingIds[_workerDeathIndex]
STR eventMessage = ""
INT totalWorkers = misc:list_size("_workerNames")
INT randomPercentage = math:random_integer(1, 100)
BOL workerIsDead = <false>

/// Update dying from childbirth
IF workerIsDead IS <false>
    INT houseIndex = misc:list_index("_houseIdsThatGaveBirth", workerHouseId)
    IF houseIndex NOT -1
        PULL _houseIdsThatGaveBirth houseIndex
        INT birthDeathChance = 100
        SUB birthDeathChance _birthDeathChance
        IF randomPercentage MORE birthDeathChance
            EDIT workerIsDead = <true>
            EDIT eventMessage = misc:string_concat(workerName, " died of childbirth")
            PUSH _eventMessages eventMessage

/// Update dying from suicide
IF workerIsDead IS <false>
    INT priestCount = misc:list_size("_priestIds")
    INT tavernCount = misc:list_size("_tavernIds")
    IF priestCount MORE 0 AND tavernCount MORE 0
        INT suicideChance = 100
        SUB suicideChance _suicideDeathChance
        IF randomPercentage MORE suicideChance
            EDIT workerIsDead = <true>
            EDIT eventMessage = misc:string_concat(workerName, " died of suicide")
            PUSH _eventMessages eventMessage

/// Update dying from sickness
IF workerIsDead IS <false>
    IF _workerHealths[_workerDeathIndex] IS <true>
        INT sicknessChance = 100
        SUB sicknessChance _sicknessCatchChance
        IF randomPercentage MORE sicknessChance
            EDIT _workerHealths[_workerDeathIndex] = <false>
            EDIT eventMessage = misc:string_concat(workerName, " caught the sickness")
            PUSH _eventMessages eventMessage
    ELSE
        IF _totalMedicine MORE 0
            SUB _totalMedicine 1
            EDIT _workerHealths[_workerDeathIndex] = <true>
        ELSE
            EDIT workerIsDead = <true>
            EDIT eventMessage = misc:string_concat(workerName, " died of sickness")
            PUSH _eventMessages eventMessage

/// Update dying from dehydration
IF workerIsDead IS <false>
    IF _totalWater MORE 0
        SUB _totalWater 1
    ELIF _totalAle MORE 0
        SUB _totalAle 1
    ELIF _totalMilk MORE 0
        SUB _totalMilk 1
    ELSE
        EDIT workerIsDead = <true>
        EDIT eventMessage = misc:string_concat(workerName, " died of dehydration")
        PUSH _eventMessages eventMessage

/// Update dying from starvation
IF workerIsDead IS <false>
    IF _totalSmokedMeat MORE 0
        SUB _totalSmokedMeat 1
    ELIF _totalSmokedFish MORE 0
        SUB _totalSmokedFish 1
    ELIF _totalBread MORE 0
        SUB _totalBread 1
    ELIF _totalVegetables MORE 0
        SUB _totalVegetables 1
    ELIF _totalEggs MORE 0
        SUB _totalEggs 1
    ELIF _totalRawMeat MORE 0
        SUB _totalRawMeat 1
    ELIF _totalRawFish MORE 0
        SUB _totalRawFish 1
    ELIF _totalHoney MORE 0
        SUB _totalHoney 1
    ELIF _totalFruit MORE 0
        SUB _totalFruit 1
    ELSE
        EDIT workerIsDead = <true>
        EDIT eventMessage = misc:string_concat(workerName, " died of starvation")
        PUSH _eventMessages eventMessage

/// Handle dead worker
IF workerIsDead IS <true>
    IF _workerNames[_workerDeathIndex] IS "MALE"
        PUSH _maleWorkerNames _workerNames[_workerDeathIndex]
    ELSE
        PUSH _femaleWorkerNames _workerNames[_workerDeathIndex]
    PULL _workerNames _workerDeathIndex
    PULL _workerGenders _workerDeathIndex
    PULL _workerHealths _workerDeathIndex
    PULL _workerJobBuildingIds _workerDeathIndex
    PULL _workerHouseBuildingIds _workerDeathIndex
    ADD _totalDeadWorkers 1
    SUB totalWorkers 1
ELSE
    ADD _workerDeathIndex 1

/// Handle dead population
IF totalWorkers IS 0
    EXIT

/// Update event index
IF _workerDeathIndex IS totalWorkers
    EDIT _workerDeathIndex = 0
    EDIT _workerDeathTimespan = _workersDeathTimespan
    DIV _workerDeathTimespan totalWorkers

27 0
META script_type_update
META script_state_waiting

/// Validate
IF _workersToCreate IS 0
  IF _isCreatingDefaultWorkers IS <true>
    ALT _isCreatingDefaultWorkers = <false>
  EXIT
ELSE
  SUB _workersToCreate 1

/// Determine name and gender
IF _nextWorkerGender IS 1
  DEF INT nameCount = misc:list_size("_maleWorkerNames")
  DEF INT nameIndex = math:random_integer(1, nameCount)
  SUB nameIndex 1
  DEF STR name = _maleWorkerNames[nameIndex]
  DEF STR gender = "MALE"
ELSE
  DEF INT nameCount = misc:list_size("_femaleWorkerNames")
  DEF INT nameIndex = math:random_integer(1, nameCount)
  SUB nameIndex 1
  DEF STR name = _femaleWorkerNames[nameIndex]
  DEF STR gender = "FEMALE"

/// Switch gender
NEG _nextWorkerGender

/// Create worker
PUSH _workerNames name
PUSH _workerGenders gender
PUSH _workerHealths <true>
PUSH _workerClothes <false>
PUSH _workerJobBuildingIds ""
PUSH _workerHouseBuildingIds ""

/// Update workers
ALT _mustUpdateWorkers = <true>

/// Log birth event
IF _isCreatingDefaultWorkers IS <false>
  DEF STR eventMessage = misc:string_concat(name, " was born")
  PUSH _eventMessageContents eventMessage
  PUSH _eventMessageTypes "birth"

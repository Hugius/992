50 40
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "weaver" OR _isFirstUpdate IS <true>
  fe3d:quad2d_set_visible("weaver_worker", <false>)
  fe3d:quad2d_set_visible("weaver_wool", <false>)
  fe3d:quad2d_set_visible("weaver_leather", <false>)
  fe3d:quad2d_set_visible("weaver_convert", <false>)
  fe3d:quad2d_set_visible("weaver_clothes", <false>)
  fe3d:text2d_set_visible("weaver_worker", <false>)
  fe3d:text2d_set_visible("weaver_wool", <false>)
  fe3d:text2d_set_visible("weaver_leather", <false>)
  fe3d:text2d_set_visible("weaver_clothes", <false>)

/// Show GUI
IF _selectedBuildingType IS "weaver"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    fe3d:quad2d_set_visible("weaver_wool", <true>)
    fe3d:quad2d_set_visible("weaver_leather", <true>)
    fe3d:quad2d_set_visible("weaver_convert", <true>)
    fe3d:quad2d_set_visible("weaver_clothes", <true>)
    fe3d:text2d_set_visible("weaver_wool", <true>)
    fe3d:text2d_set_visible("weaver_leather", <true>)
    fe3d:text2d_set_visible("weaver_clothes", <true>)
    fe3d:text2d_set_visible("weaver_clothes", <true>)
  ELSE
    fe3d:quad2d_set_visible("weaver_worker", <true>)
    fe3d:text2d_set_visible("weaver_worker", <true>)

/// Update weavers
DEF INT maxWeaverIndex = misc:list_size("_weaverIds")
DEF INT waeverIndex = 0
DEF INT randomNumber = 0
LOOP
  IF waeverIndex IS maxWeaverIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _weaverIds[waeverIndex])
  IF hasWorker IS <true>
    IF _weaverTimers[waeverIndex] IS 0
      ALT randomNumber = math:random_integer(1, 2)
      IF randomNumber IS 1 AND _totalWool MORE 0
        SUB _totalWool 1
        ADD _weaverTimers[waeverIndex] 1
      ELIF randomNumber IS 2 AND _totalLeather MORE 0
        SUB _totalLeather 1
        ADD _weaverTimers[waeverIndex] 1
    ELIF _weaverTimers[waeverIndex] IS _productionTime
      ALT _weaverTimers[waeverIndex] = 0
      ADD _totalClothes 1
    ELSE
      ADD _weaverTimers[waeverIndex] 1
  ADD waeverIndex 1

13 29
META script_type_update
META script_state_waiting

/// Save build mode
ALT _wasBuildModeEnabled = _isBuildModeEnabled

/// Update build button
DEF BOL isButtonHovered = fe3d:quad2d_is_hovered("build")
IF isButtonHovered IS <true>
  fe3d:quad2d_set_lightness("build", 1.25)
  ALT _cursorType = "HAND"
  DEF BOL isLmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
  IF isLmbPressed IS <true>
    fe3d:sound2d_start("click", 1, 0.0)
    IF _isBuildModeEnabled IS <true>
      fe3d:model_set_visible(_buildingTypes[_buildingIndex], <false>)
      ALT _isBuildModeEnabled = <false>
    ELSE
      fe3d:model_set_visible(_buildingTypes[_buildingIndex], <true>)
      ALT _isBuildModeEnabled = <true>
ELSE
  fe3d:quad2d_set_lightness("build", 1.0)

/// Update GUI
fe3d:quad2d_set_visible("building_price_type", _isBuildModeEnabled)
fe3d:quad2d_set_visible("building_price_planks", _isBuildModeEnabled)
fe3d:quad2d_set_visible("building_price_bricks", _isBuildModeEnabled)
fe3d:quad2d_set_visible("building_price_clay", _isBuildModeEnabled)
fe3d:quad2d_set_visible("building_price_straw", _isBuildModeEnabled)
fe3d:text2d_set_visible("building_price_type", _isBuildModeEnabled)
fe3d:text2d_set_visible("building_price_planks", _isBuildModeEnabled)
fe3d:text2d_set_visible("building_price_bricks", _isBuildModeEnabled)
fe3d:text2d_set_visible("building_price_clay", _isBuildModeEnabled)
fe3d:text2d_set_visible("building_price_straw", _isBuildModeEnabled)

/// Validate
IF _isBuildModeEnabled IS <false>
  EXIT

/// Update GUI
DEF STR planksPriceText = misc:string_zeroed_integer(_buildingPricesPlanks[_buildingIndex], 4)
DEF STR bricksPriceText = misc:string_zeroed_integer(_buildingPricesBricks[_buildingIndex], 4)
DEF STR clayPriceText = misc:string_zeroed_integer(_buildingPricesClay[_buildingIndex], 4)
DEF STR strawPriceText = misc:string_zeroed_integer(_buildingPricesStraw[_buildingIndex], 4)
DEF STR textContent = misc:string_capitalize(_buildingTypes[_buildingIndex])
DEF INT textWidth = misc:string_size(textContent)
CAST textWidth DEC
MUL textWidth 0.03
fe3d:text2d_set_content("building_price_type", textContent)
fe3d:text2d_set_size("building_price_type", textWidth, 0.15)
fe3d:text2d_set_content("building_price_planks", planksPriceText)
fe3d:text2d_set_content("building_price_bricks", bricksPriceText)
fe3d:text2d_set_content("building_price_clay", clayPriceText)
fe3d:text2d_set_content("building_price_straw", strawPriceText)

/// Hide last building
fe3d:model_set_visible(_buildingTypes[_buildingIndex], <false>)

/// Update building index
DEF INT buildingCount = misc:list_size("_buildingTypes")
DEF BOL hasScrolledUp = fe3d:input_is_mouse_scrolled("WHEEL_UP")
DEF BOL hasScrolledDown = fe3d:input_is_mouse_scrolled("WHEEL_DOWN")
IF hasScrolledUp IS <true>
  fe3d:sound2d_start("scroll", 1, 0.0)
  ADD _buildingIndex 1
  IF _buildingIndex IS buildingCount
    ALT _buildingIndex = 0
IF hasScrolledDown IS <true>
  fe3d:sound2d_start("scroll", 1, 0.0)
  IF _buildingIndex IS 0
    ALT _buildingIndex = buildingCount
  SUB _buildingIndex 1

/// Show current building
fe3d:model_set_visible(_buildingTypes[_buildingIndex], <true>)

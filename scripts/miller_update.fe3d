0 0
META script_type_update
META script_state_waiting

/// Define reusable values
DEF BOL hasWorker = <false>

/// Hide GUI
IF _lastSelectedBuildingType IS "miller" OR _isFirstUpdate IS <true>
  fe3d:quad2d_set_visible("miller_worker", <false>)
  fe3d:quad2d_set_visible("miller_grain", <false>)
  fe3d:quad2d_set_visible("miller_convert", <false>)
  fe3d:quad2d_set_visible("miller_flour", <false>)
  fe3d:text2d_set_visible("miller_worker", <false>)
  fe3d:text2d_set_visible("miller_grain", <false>)
  fe3d:text2d_set_visible("miller_flour", <false>)

/// Show GUI
IF _selectedBuildingType IS "miller"
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _selectedBuildingId)
  IF hasWorker IS <true>
    fe3d:quad2d_set_visible("miller_grain", <true>)
    fe3d:quad2d_set_visible("miller_convert", <true>)
    fe3d:quad2d_set_visible("miller_flour", <true>)
    fe3d:text2d_set_visible("miller_grain", <true>)
    fe3d:text2d_set_visible("miller_flour", <true>)
  ELSE
    fe3d:quad2d_set_visible("miller_worker", <true>)
    fe3d:text2d_set_visible("miller_worker", <true>)

/// Update millers
DEF INT maxMillerIndex = misc:list_size("_millerIds")
DEF INT millerIndex = 0
LOOP
  IF millerIndex IS maxMillerIndex
    BREAK
  ALT hasWorker = misc:list_contains("_workerJobBuildingIds", _millerIds[millerIndex])
  IF hasWorker IS <true>
    IF _millerTimers[millerIndex] IS 0
      IF _totalGrain MORE 0
        SUB _totalGrain 1
        ADD _millerTimers[millerIndex] 1
    ELIF _millerTimers[millerIndex] IS _productionTime
      ALT _millerTimers[millerIndex] = 0
      ADD _totalFlour 1
    ELSE
      ADD _millerTimers[millerIndex] 1
  ADD millerIndex 1
